namespace drr.regulation.asic.rewrite.margin
version "${project.version}"

import cdm.base.datetime.*
import cdm.base.staticdata.identifier.*
import cdm.base.staticdata.party.*
import cdm.product.collateral.*

import drr.regulation.asic.*
import drr.regulation.common.*
import drr.standards.iosco.cde.version3.* as cde
import drr.standards.iso.*

corpus Dissemination Margin

report ASIC Margin in T+1
    from CollateralReportInstruction
    when IsCollateralReportable
    with type ASICMarginReport

eligibility rule IsCollateralReportable from CollateralReportInstruction: <"When eligible for ASIC">
    [regulatoryReference ASIC Margin article "9" paragraph "1"
        provision "Counterparties and CCPs shall ensure that the details of any derivative contract they have concluded and of any modification or termination of the contract are reported to a trade repository registered in accordance with Article 55 or recognised in accordance with Article 77. The details shall be reported no later than the working day following the conclusion, modification or termination of the contract."]
    filter HasCollateral

eligibility rule HasCollateral from CollateralReportInstruction: <"When eligible for ASIC">
    [regulatoryReference ASIC Margin article "2" paragraph "5"
        provision "Derivative or derivative contract means a financial instrument as set out in points (4) to (10) of Section C of Annex I to Directive 2004/39 EC as implemented by Article 38 and 39 of Regulation (EC) No 1287/2006"]
    extract collateralDetails -> collateral exists

//ASIC Definitions
reporting rule ASICUniqueTransactionIdentifier from CollateralReportInstruction: <"Unique transaction identifier">
    [regulatoryReference ASIC Margin table "3" dataElement "1" field "Unique transaction identifier"
        provision "For a report made under Rule 2.2.2, if one transaction identifier was reported for the Reportable Transaction, the transaction identifier.
            If more than one transaction identifier was reported for the Reportable Transaction, one of those transaction identifiers."]
    extract
        if GetRegimeSpecificIdentifiers(
                reportableInformation,
                SupervisoryBodyEnum -> ASIC,
                TradeIdentifierTypeEnum -> UniqueTransactionIdentifier
            )
            exists
        then GetRegimeSpecificIdentifiers(
                    reportableInformation,
                    SupervisoryBodyEnum -> ASIC,
                    TradeIdentifierTypeEnum -> UniqueTransactionIdentifier
                )
        else collateralDetails -> uniqueTradeIdentifier -> assignedIdentifier -> identifier
    then distinct
    then only-element

reporting rule UniqueTransactionIdentifier from CollateralReportInstruction: <"Unique transaction identifier">
    [regulatoryReference ASIC Margin table "3" dataElement "1" field "Unique transaction identifier"
        provision "For a report made under Rule 2.2.2, if one transaction identifier was reported for the Reportable Transaction, the transaction identifier.
            If more than one transaction identifier was reported for the Reportable Transaction, one of those transaction identifiers."]
    ASICUniqueTransactionIdentifier then filter IsMax32UpperCaseAlphanumericText = True
        as "10 Unique Transaction Identifier (UTI)"

reporting rule UniqueTransactionIdentifierProprietary from CollateralReportInstruction: <"Unique transaction identifier">
    [regulatoryReference ASIC Margin table "3" dataElement "1" field "Unique transaction identifier"
        provision "For a report made under Rule 2.2.2, if one transaction identifier was reported for the Reportable Transaction, the transaction identifier.
            If more than one transaction identifier was reported for the Reportable Transaction, one of those transaction identifiers."]
    ASICUniqueTransactionIdentifier then filter IsMax32UpperCaseAlphanumericText = False
        as "10 Unique Transaction Identifier (Proprietary)"

reporting rule ReportingEntity from CollateralReportInstruction: <"Reporting entity">
    [regulatoryReference ASIC Margin table "3" dataElement "2" field "Reporting entity"
        provision "The current LEI of the Reporting Entity"]
    extract ExtractPartyResponsibleForReportingIdentifier(reportingSide)
        as "3.2 Reporting entity"

reporting rule Counterparty1 from CollateralReportInstruction: <"Counterparty 1">
    [regulatoryReference ASIC Margin table "3" dataElement "3" field "Counterparty 1"
        provision "The current LEI of:
            If the Reporting Entity is an RE, Trustee or corporate director of the managed investment scheme, trust or CCIV that holds the OTC Derivative the subject of the Reportable Transaction, the managed investment scheme, trust or CCIV.
            Otherwise, the Reporting Entity."]
    extract reportingSide -> reportingParty
    then extract partyId -> identifier only-element
        as "3.3 Counterparty 1"

reporting rule Counterparty2IdentifierType from CollateralReportInstruction: <"Counterparty 2 identifier type">
    [regulatoryReference ASIC Margin table "3" dataElement "3_1" field "Counterparty 2 identifier type"
        provision "Indicator of whether LEI was used to identify the counterparty 2."]
    extract reportingSide -> reportingCounterparty
    then extract
        if partyId -> identifierType only-element = PartyIdentifierTypeEnum -> LEI
        then True
        else False
        as "3.3_1 Counterparty 2 identifier type"

reporting rule SmallScaleBuySideEntityIndicator from CollateralReportInstruction: <"Small-scale Buy-side Entity indicator">
    [regulatoryReference ASIC Margin table "3" dataElement "4" field "Small-scale Buy-side Entity indicator"
        provision "If applicable, an indicator that the Reporting Entity is making the report as a Small-scale Buy-side Entity."]
    extract item -> reportableInformation -> partyInformation
    then extract regimeInformation
    then flatten
    then filter supervisoryBody = SupervisoryBodyEnum -> ASIC
    then only-element
    then extract
        if asicPartyInformation -> smallScaleBuySideIndicator exists
                and asicPartyInformation -> smallScaleBuySideIndicator = True
        then "SSBS"
        as "3.4 Small-scale Buy-side Entity indicator"

reporting rule Counterparty2 from CollateralReportInstruction: <"Counterparty 2">
    [regulatoryReference ASIC Margin table "3" dataElement "5" field "Counterparty 2"
        provision "The LEI or another identifier, determined in accordance with subrule S1.3.1(2), of the entity that is:
            If the counterparty of the OTC Derivative the subject of the Reportable Transaction whose identifier is not reported at Counterparty 1 (item 3 above) is an RE or Trustee of the managed investment scheme or trust that holds the OTC Derivative the subject of the Reportable Transaction, the managed investment scheme or trust;
            Otherwise, the counterparty of the OTC Derivative the subject of the Reportable Transaction whose identifier is not reported at Counterparty 1 (item 3 above)."]
    extract reportingSide -> reportingCounterparty
    then extract partyId -> identifier only-element
        as "3.5 Counterparty 2"

reporting rule CollateralTimestamp from CollateralReportInstruction: <"Collateral timestamp">
    [regulatoryReference ASIC Margin table "3" dataElement "6" field "Collateral timestamp"
        provision "Latest date, or date and time, that an amount reported for items 12, 13, 15, 16, 18 or 20 was determined."]
    extract collateralDetails -> collateralTimestamp
        as "3.6 Collateral timestamp"

reporting rule CollateralisationCategory from CollateralReportInstruction: <"Collateralisation category">
    [regulatoryReference ASIC Margin table "3" dataElement "7" field "Collateralisation category"
        provision "An indicator of whether the OTC Derivative the subject of the Reportable Transaction is included in a collateral agreement (or collateral agreements) between the counterparties and of the kind of the collateral posting arrangements under the collateral agreement (or collateral agreements)."]
    extract
        item -> collateralDetails -> collateralisationCategory to-enum CollateralisationType3Code__1
        as "3.7 Collateralisation category"

reporting rule CollateralPortfolioIndicator from CollateralReportInstruction: <"Collateral portfolio indicator">
    [regulatoryReference ASIC Margin table "3" dataElement "8" field "Collateral portfolio indicator"
        provision "If the OTC Derivative the subject of the Reportable Transaction is collateralised, the True or False indicator specified in the applicable paragraph of column 5 of this item"]
    extract collateralDetails -> collateral -> portfolioIdentifier exists
        as "3.8 Collateral portfolio indicator"

reporting rule PortfolioContainingNonReportedComponentIndicator from CollateralReportInstruction: <"Portfolio containing non-reported component indicator">
    [regulatoryReference ASIC Margin table "3" dataElement "9" field "Portfolio containing non-reported component indicator"
        provision "An indicator of whether the collateral portfolio in which the OTC Derivative the subject of the Reportable Transaction includes transactions that are not reported under these Rules."]
    extract item -> reportableInformation -> partyInformation
    then extract regimeInformation
    then flatten
    then filter supervisoryBody = SupervisoryBodyEnum -> ASIC
    then only-element
    then extract
        if asicPartyInformation -> nonReportedTradePortfolio exists
        then asicPartyInformation -> nonReportedTradePortfolio
        else False
        as "3.9 Portfolio containing non-reported component indicator"

reporting rule CollateralPortfolioCodeIM from CollateralReportInstruction: <"Collateral portfolio code (IM)">
    [regulatoryReference ASIC Margin table "3" dataElement "10" field "Collateral portfolio code (IM)"
        provision "If the OTC Derivative the subject of the Reportable Transaction is collateralised and collateral is reported on a Portfolio Basis, a unique code, determined by the Reporting Entity to identify the collateral portfolio of amounts of initial margin, unless the collateralisation does not involve amounts of initial margin.
            If there is only one collateral portfolio of amounts of margin that does not distinguish between margin that is initial margin and margin that is variation margin, the unique code is of that portfolio."]
    extract collateralDetails -> collateral -> collateralPortfolio
    then filter
        legalAgreement -> legalAgreementIdentification -> agreementName -> creditSupportAgreementMarginType = CollateralMarginTypeEnum -> InitialMargin
    then only-element
    then portfolioIdentifier -> assignedIdentifier -> identifier last
        as "3.10 Collateral portfolio code (IM)"

reporting rule CollateralPortfolioCodeVM from CollateralReportInstruction: <"Collateral portfolio code (VM)">
    [regulatoryReference ASIC Margin table "3" dataElement "11" field "Collateral portfolio code (VM)"
        provision "If the OTC Derivative the subject of the Reportable Transaction is collateralised and collateral is reported on a Portfolio Basis, a unique code, determined by the Reporting Entity to identify the collateral portfolio of amounts of variation margin, unless the collateralisation does not involve amounts of variation margin.
            This data element is not reported if there is only one collateral portfolio of amounts of margin that does not distinguish between margin that is initial margin and margin that is variation margin, and the collateral portfolio code of the collateral portfolio is reported as Collateral portfolio code (initial margin) (item 10 above)."]
    extract collateralDetails -> collateral -> collateralPortfolio
    then filter
        legalAgreement -> legalAgreementIdentification -> agreementName -> creditSupportAgreementMarginType = CollateralMarginTypeEnum -> VariationMargin
    then only-element
    then portfolioIdentifier -> assignedIdentifier -> identifier last
        as "3.11 Collateral portfolio code (VM)"

reporting rule InitialMarginPostedByTheReportingEntityPreHaircut from CollateralReportInstruction: <"Initial margin posted by the Reporting Entity (pre-haircut) ">
    [regulatoryReference ASIC Margin table "3" dataElement "12" field "Initial margin posted by the Reporting Entity (pre-haircut)"
        provision "The monetary value of initial margin that has been posted by the Reporting Entity to the other counterparty.
            This refers to the total current value of the initial margin rather than to its daily change.
            Where a single amount of margin is posted that does not distinguish between margin that is initial margin and margin that is variation margin, the single amount of margin is reported for this data element"]
    extract
        cde.collateral.InitialMarginPostedByReportingCounterpartyPreHaircut(
                item,
                reportingSide -> reportingParty
            )
        as "3.12 Initial margin posted by the Reporting Entity (pre-haircut)"

reporting rule InitialMarginPostedByTheReportingEntityPostHaircut from CollateralReportInstruction: <"Initial margin posted by the Reporting Entity (post-haircut) ">
    [regulatoryReference ASIC Margin table "3" dataElement "13" field "Initial margin posted by the Reporting Entity (post-haircut)"
        provision "The monetary value that is the amount reported for Initial margin posted by the Reporting Entity (pre-haircut) (item 12 above) less the amount, if any, (the haircut) that is not counted as satisfying a term of margin posting of the collateralisation arrangement"]
    extract
        cde.collateral.InitialMarginPostedByReportingCounterpartyPostHaircut(
                item,
                reportingSide -> reportingParty
            )
        as "3.13 Initial margin posted by the Reporting Entity (post-haircut)"

reporting rule CurrencyOfInitialMarginPosted from CollateralReportInstruction: <"Currency of initial margin posted">
    [regulatoryReference ASIC Margin table "3" dataElement "14" field "Currency of initial margin posted"
        provision "The currency code of the currency in which the amount reported for Initial margin posted by the Reporting Entity (pre-haircut) (item 12 above) is denominated."]
    extract
        cde.collateral.InitialMarginPostedByReportingCounterpartyCurrency(
                item,
                reportingSide -> reportingParty
            )
        as "3.14 Currency of initial margin posted"

reporting rule InitialMarginCollectedByTheReportingEntityPreHaircut from CollateralReportInstruction: <"Initial margin collected by the Reporting Entity (pre-haircut)">
    [regulatoryReference ASIC Margin table "3" dataElement "15" field "Initial margin collected by the Reporting Entity (pre-haircut)"
        provision "The monetary value of initial margin that has been collected by the Reporting Entity from the other counterparty.
        This refers to the total current value of the initial margin rather than to its daily change.
        Where a single amount of margin is collected that does not distinguish between margin that is initial margin and margin that is variation margin, the single amount of margin is reported for this data element."]
    extract
        cde.collateral.InitialMarginCollectedByReportingCounterpartyPreHaircut(
                item,
                reportingSide -> reportingParty
            )
        as "3.15 Initial margin collected by the Reporting Entity (pre-haircut)"

reporting rule InitialMarginCollectedByTheReportingEntityPostHaircut from CollateralReportInstruction: <"Initial margin collected by the Reporting Entity (post-haircut)">
    [regulatoryReference ASIC Margin table "3" dataElement "16" field "Initial margin collected by the Reporting Entity (post-haircut)"
        provision "The monetary value that is the amount reported for Initial margin collected by the Reporting Entity (pre-haircut) (item 15 above) less the amount, if any, (the haircut) that is not counted as satisfying a term of margin posting of the collateralisation arrangement."]
    cde.collateral.InitialMarginCollectedByReportingCounterpartyPostHaircut(
            item,
            reportingSide -> reportingParty
        )
        as "3.16 Initial margin collected by the Reporting Entity (post-haircut)"

reporting rule CurrencyOfInitialMarginCollected from CollateralReportInstruction: <"Currency of initial margin collected">
    [regulatoryReference ASIC Margin table "3" dataElement "17" field "Currency of initial margin collected"
        provision "The currency code of the currency in which the amount reported for Initial margin collected by the Reporting Entity (pre-haircut) (item 15 above) is denominated."]
    extract
        cde.collateral.InitialMarginCollectedByReportingCounterpartyCurrency(
                item,
                reportingSide -> reportingParty
            )
        as "3.17 Currency of initial margin collected"

reporting rule VariationMarginPostedByTheReportingEntityPreHaircut from CollateralReportInstruction: <"Variation margin posted by the Reporting Entity (pre-haircut)">
    [regulatoryReference ASIC Margin table "3" dataElement "18" field "Variation margin posted by the Reporting Entity (pre-haircut)"
        provision "The monetary value of variation margin that has been posted by the Reporting Entity to the other counterparty.
        This refers to the total current value of the variation margin rather than to its daily change"]
    extract
        cde.collateral.VariationMarginPostedByReportingCounterpartyPreHaircut(
                item,
                reportingSide -> reportingParty
            )
        as "3.18 Variation margin posted by the Reporting Entity (pre-haircut)"

reporting rule CurrencyOfVariationMarginPosted from CollateralReportInstruction: <"Currency of variation margin posted">
    [regulatoryReference ASIC Margin table "3" dataElement "19" field "Currency of variation margin posted"
        provision "The currency code of the currency in which the amount reported for variation margin posted by the Reporting Entity (pre-haircut) (item 18 above) is denominated."]
    extract
        cde.collateral.VariationMarginPostedByReportingCounterpartyCurrency(
                item,
                reportingSide -> reportingParty
            )
        as "3.19 Currency of variation margin posted."

reporting rule VariationMarginCollectedByTheReportingEntityPreHaircut from CollateralReportInstruction: <"Variation margin collected by the Reporting Entity (pre-haircut)">
    [regulatoryReference ASIC Margin table "3" dataElement "20" field "Variation margin collected by the Reporting Entity (pre-haircut)"
        provision "The monetary value of variation margin that has been collected by the Reporting Entity from the other counterparty.
        This refers to the total current value of the variation margin rather than to its daily change"]
    extract
        cde.collateral.VariationMarginCollectedByReportingCounterpartyPreHaircut(
                item,
                reportingSide -> reportingParty
            )
        as "3.20 Variation margin collected by the Reporting Entity (pre-haircut)"

reporting rule CurrencyOfVariationMarginCollected from CollateralReportInstruction: <"Currency of variation margin collected">
    [regulatoryReference ASIC Margin table "3" dataElement "21" field "Currency of variation margin collected"
        provision "The currency code of the currency in which the amount reported for variation margin collected by the Reporting Entity (pre-haircut) (item 20 above) is denominated."]
    extract
        cde.collateral.VariationMarginCollectedByReportingCounterpartyCurrency(
                item,
                reportingSide -> reportingParty
            )
        as "3.21 Currency of variation margin collected."

reporting rule ActionType from CollateralReportInstruction: <"Action type">
    [regulatoryReference ASIC Margin table "3" dataElement "22" field "Action type"
        provision "An indicator of whether the report being made relates to:
        (a) reporting collateral arrangements information in a Reportable Transaction that is the entry into an OTC Derivative;
        (b) the modification of collateral arrangements information in a report previously made;
        (c) an update of collateral amounts information; or
        (d) a correction to collateral arrangements information or collateral amounts information in a report previously made."]
    extract
        if collateralDetails -> action = MarginActionEnum -> MARU
        then collateralDetails -> action
        as "3.22 Action type"

reporting rule ReportingSubmittingEntity from CollateralReportInstruction: <"Report submitting entity">
    [regulatoryReference ASIC Margin table "3" dataElement "24" field "Report submitting entity"
        provision "The LEI of the person submitting the report of information set out in this table."]
    extract ExtractReportSubmittingPartyIdentifier(reportingSide)
        as "3.24 Report submitting entity"

reporting rule TechnicalRecordId from CollateralReportInstruction: <"Technical Record Id (ISO)">
    [regulatoryReference ASIC Margin dataElement "[Internal]" field "Technical Record Id (ISO)"
        provision "Unique technical identification of the original data for which the status is provided for the BDR."]
    extract reportableInformation -> partyInformation -> regimeInformation
    then filter regimeName = RegimeNameEnum -> ASIC
    then extract technicalRecordId
    then distinct only-element
        as "[Internal] Technical Record Id (ISO)"

reporting rule ReportingTimestamp from CollateralReportInstruction: <"Reporting timestamp">
    [regulatoryReference ASIC Margin dataElement "[Not in regulation]" field "Reporting timestamp"
        provision "The date and time of the submission of a report of information set out in this table to the Derivative Trade Repository."]
    Now
        as "[Not in regulation] Reporting timestamp"

reporting rule ExecutionAgentOfCounterparty1 from CollateralReportInstruction: <"Execution agent of the Counterparty 1 (DTCC)">
    [regulatoryReference ASIC Margin dataElement "N/A" field "Execution agent of Counterparty 1"
        provision "If applicable, the LEI of the entity that entered into the Reportable Transaction as agent for the Reporting Entity without becoming a counterparty themselves."]
    extract reportableCollateral [
        reportableCollateral -> reportableInformation -> partyInformation
            filter partyReference = reportableCollateral -> reportingSide -> reportingParty
            then distinct only-element
            then extract
                ExtractPartyFromRelatedPartyByRole(
                        relatedParty,
                        PartyRoleEnum -> ExecutionAgent
                    )
            then extract PartyLei(partyId)
    ]
        as "N/A Execution agent of Counterparty 1"
