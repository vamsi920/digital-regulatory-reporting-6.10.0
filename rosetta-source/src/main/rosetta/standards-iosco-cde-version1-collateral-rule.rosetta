namespace drr.standards.iosco.cde.version1.collateral
version "${project.version}"

import cdm.base.*

import drr.regulation.common.*
import drr.standards.iosco.*
import drr.standards.iosco.cde.version1.* as cdeV1

reporting rule CollateralPortfolioIndicator from TransactionReportInstruction: <"Collateral Portfolio Indicator">
    [regulatoryReference CPMI_IOSCO cdeV1.CDE table "2" field "29"
        provision "Indicator of whether the collateralisation was performed on a portfolio basis. Under portfolio it is understood the set of transactions that are margined together (either on a net or a gross basis) rather than on the basis of each individual transaction."]
    extract PositionForEvent -> collateral default TradeForEvent -> collateral
    then portfolioIdentifier exists or collateralPortfolio -> portfolioIdentifier exists

reporting rule CollateralPortfolioCode from TransactionReportInstruction: <"Collateral Portfolio Code">
    [regulatoryReference CPMI_IOSCO cdeV1.CDE table "2" field "30"
        provision "If collateral is reported on a portfolio basis, unique code assigned by counterparty 1 to the portfolio. This data element is not applicable if the collateralisation was performed on a transaction level basis, or if there is no collateral agreement or if no collateral is posted or received."]
    extract
        if PositionForEvent -> collateral -> portfolioIdentifier -> assignedIdentifier exists
        then PositionForEvent -> collateral -> portfolioIdentifier -> assignedIdentifier
        else if TradeForEvent -> collateral -> portfolioIdentifier -> assignedIdentifier exists
        then TradeForEvent -> collateral -> portfolioIdentifier -> assignedIdentifier
    then distinct only-element
    then extract identifier

reporting rule ExcessCollateralPostedByTheReportingCounterparty from CollateralReportInstruction: <"Excess collateral posted by the reporting counterparty">
    [regulatoryReference CPMI_IOSCO cdeV1.CDE section "2" field "43"
        provision "Monetary value of any additional collateral posted by the reporting counterparty separate and independent from initial and variation margin. This refers to the total current value of the excess collateral before application of the haircut (if applicable), rather than to its daily change.
          Any initial or variation margin amount posted that exceeds the required initial margin or required variation margin, is reported as part of the initial margin posted or variation margin posted respectively rather than included as excess collateral posted.
          For centrally cleared transactions, excess collateral is reported only to the extent it can be assigned to a specific portfolio or transaction."]
    [regulatoryReference ISDA EMEADataAndReporting date "20230206"
        provision "No scenarios identified, this field will not be populated until further guidance from the ROC and/or ESMA."]
    0 then FormatToShortFraction5DecimalNumber

reporting rule CurrencyOfExcessCollateralPosted from CollateralReportInstruction: <"Currency of excess collateral posted">
    [regulatoryReference CPMI_IOSCO cdeV1.CDE section "2" field "44"
        provision "Currency in which the excess collateral posted is denominated.
          If the excess collateral posted is denominated in more than one currency, this data element reflects one of those currencies into which the reporting counterparty has chosen to convert all the values of posted excess collateral."]
    [regulatoryReference ISDA EMEADataAndReporting date "20230206"
        provision "No scenarios identified, this field will not be populated until further guidance from the ROC and/or ESMA."]
    ""

reporting rule ExcessCollateralCollectedByTheReportingCounterparty from CollateralReportInstruction: <"Excess collateral collected by the reporting counterparty">
    [regulatoryReference CPMI_IOSCO cdeV1.CDE section "2" field "45"
        provision "Monetary value of any additional collateral collected by the reporting counterparty separate and independent from initial and variation margin. This data element refers to the total current value of the excess collateral before application of the haircut (if applicable), rather than to its daily change.
          Any initial or variation margin amount collected that exceeds the required initial margin or required variation margin, is reported as part of the initial margin collected or variation margin collected respectively, rather than included as excess collateral collected.
          For centrally cleared transactions excess collateral is reported only to the extent it can be assigned to a specific portfolio or transaction."]
    [regulatoryReference ISDA EMEADataAndReporting date "20230206"
        provision "No scenarios identified, this field will not be populated until further guidance from the ROC and/or ESMA."]
    0 then FormatToShortFraction5DecimalNumber

reporting rule CurrencyOfExcessCollateralCollected from CollateralReportInstruction: <"Currency of excess collateral posted">
    [regulatoryReference CPMI_IOSCO cdeV1.CDE section "2" field "46"
        provision "Currency in which the excess collateral collected is denominated.
          If the excess collateral is denominated in more than one currency, this data element reflects one of those currencies into which the reporting counterparty has chosen to convert all the values of collected excess collateral"]
    [regulatoryReference ISDA EMEADataAndReporting date "20230206"
        provision "No scenarios identified, this field will not be populated until further guidance from the ROC and/or ESMA."]
    ""

reporting rule CollateralisationCategory from CollateralReportInstruction: <"Collateralisation category">
    [regulatoryReference CPMI_IOSCO cdeV1.CDE section "2" dataElement "47" field "Collateralisation category"
        provision "Indicator of whether a collateral agreement (or collateral agreements) between the counterparties exists (uncollateralised/partially collateralised/one-way collateralised/fully collateralised). This data element is provided for each transaction or each portfolio, depending on whether the collateralisation is performed at the transaction or portfolio level, and is applicable to both cleared and uncleared transactions."]
    collateralDetails -> collateralisationCategory
