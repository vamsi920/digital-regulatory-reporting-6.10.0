namespace drr.standards.iosco.cde.version1.valuation
version "${project.version}"

import cdm.base.math.*
import cdm.event.common.*

import drr.regulation.common.*
import drr.standards.iosco.*
import drr.standards.iosco.cde.version1.* as cdeV1
import drr.standards.iso.*

reporting rule ValuationAmount from TransactionReportInstruction: <"Valuation amount">
    [regulatoryReference CPMI_IOSCO cdeV1.CDE section "2" field "25"
        provision "Current value of the outstanding contract. without applying any valuation adjustments (some examples include XVA adjustment such as CVA, DVA, etc).
        Valuation amount is expressed as the exit cost of the contract or components of the contract, ie the price that would be received to sell the contract (in the market in an orderly transaction at the valuation date)."]
    GetValuation -> amount -> value
        then extract RoundToPrecision(item, 5, RoundingDirectionEnum -> Nearest)
        then FormatToShortFraction5DecimalNumber
        as "2.21 Valuation amount"

reporting rule ValuationCurrency from TransactionReportInstruction: <"Valuation currency">
    [regulatoryReference CPMI_IOSCO cdeV1.CDE section "2" field "26"
        provision "Currency in which the valuation amount is denominated."]
    GetValuation -> amount -> unit -> currency then extract ConvertNonISOToISOCurrency
        as "2.22 Valuation currency"

reporting rule ValuationMethod from Valuation: <"Valuation Method">
    [regulatoryReference CPMI_IOSCO cdeV1.CDE table "2" field "28"
        provision "Source and method used for the valuation of the transaction by the reporting counterparty.
            If at least one valuation input is used that is classified as mark-to-model in the below table, then the whole valuation is classified as mark-to-model.
            If only inputs are used that are classified as mark-to-market in the table below, then the whole valuation is classified as mark-to-market."]
    if source exists and source = ValuationSourceEnum -> CentralCounterparty
    then ValuationType1Code -> CCPV
    else if method exists and method = ValuationTypeEnum -> MarkToMarket
    then ValuationType1Code -> MTMA
    else if method exists and method = ValuationTypeEnum -> MarkToModel
    then ValuationType1Code -> MTMO
