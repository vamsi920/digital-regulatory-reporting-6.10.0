namespace drr.standards.iso
version "${project.version}"

func API_GetMicData: <"Function that looks up MIC (iso10383) data based on MIC code.">
    [codeImplementation]
    // external_api https://www.iso20022.org/sites/default/files/ISO10383_MIC/ISO10383_MIC.xml
    inputs:
        mic string (0..1)
    output:
        micData MicData (0..1)

func GetOrFetchMicData: <"Return pre-enriched MIC data if present, otherwise attempt to fetch MCI data from API.">
    inputs:
        preEnriched MicData (0..*) <"Pre-enriched MIC data.">
        mic string (0..1) <"MIC to be fetched from API.">
    output:
        micData MicData (0..1)

    alias preEnrichedMicData:
        preEnriched
            filter item -> mic = mic
            then only-element

    alias isEnabled:
        // if no MIC is provided, cannot get or fetch MCI data
        mic exists
            and ( // if pre-enrich data is provided for given MIC, then use the enabled flag from that data
            (preEnrichedMicData exists and preEnrichedMicData -> micValidation default True)
                    // if pre-enrich data is not provided for given MIC, then check if blank MIC data is provided with enabled flag
                or (preEnrichedMicData is absent
                    and (preEnriched
                        filter item -> mic is absent
                        then extract item -> micValidation
                        then distinct only-element default True
                        )))

    set micData:
        if isEnabled = False
        then empty
        else if preEnrichedMicData exists
        then preEnrichedMicData
        else API_GetMicData(mic)
