namespace drr.regulation.fca.ukemir.refit.margin
version "${project.version}"

import cdm.base.*
import cdm.base.datetime.*
import cdm.base.staticdata.party.*

import drr.regulation.common.*
import drr.regulation.fca.*
import drr.regulation.fca.ukemir.*
import drr.standards.iosco.cde.version3.* as cde
import drr.standards.iso.*

corpus Dissemination Margin

report FCA UKEMIR Margin in T+1
    from CollateralReportInstruction
    when IsCollateralReportable
    with type FCAUKEMIRMarginReport

// Test Eligibility Rule
eligibility rule IsCollateralReportable from CollateralReportInstruction: <"When eligible for EMIR">
    [regulatoryReference FCA UKEMIR Margin article "4" paragraph "1"
        provision "The data on collateral for both cleared and non-cleared derivatives shall include all posted and received collateral in accordance with fields 1 to 29 in Table 3 of the Annex"]
    extract True

//UK EMIR Definitions
reporting rule ReportingTimestamp from CollateralReportInstruction: <"Reporting Timestamp">
    [regulatoryReference FCA UKEMIR table "3" dataElement "1" field "Reporting timestamp"
        provision "Date and time of the submission of the report to the trade repository."]
    Now
        as "3.1 Reporting Timestamp"

reporting rule ReportSubmittingEntityID from CollateralReportInstruction: <"Report Submitting entity ID ">
    [regulatoryReference FCA UKEMIR Margin table "3" dataElement "2" field "Report submitting entity ID"
        provision "In the case where the entity responsible for reporting has delegated the submission of the report to a third party or to the other counterparty, that entity has to be identified in this field by a unique code. Otherwise the entity responsible for reporting should be identified in this field."]
    extract ExtractReportSubmittingPartyIdentifier(reportingSide)
        as "3.2 Report Submitting entity ID"

reporting rule EntityResponsibleForReporting from CollateralReportInstruction: <"Entity responsible for reporting">
    [regulatoryReference FCA UKEMIR table "3" dataElement "3" field "Entity responsible for reporting"
        provision "Where a financial counterparty is solely responsible, and legally liable, for reporting on behalf of both counterparties in accordance with Article 9(1a) of Regulation (EU) No 648/2012 and the non-financial counterparty does not decide to report itself the details of its OTC derivative contracts with the financial counterparty, the unique code identifying that financial counterparty. Where a management company is responsible, and legally liable, for reporting on behalf of an Undertaking for Collective Investment in Transferable Securities (UCITS) in accordance with Article 9(1b) of that Regulation, the unique code identifying that management company. Where an Alternative Investment Fund Manager (AIFM) is responsible, and legally liable, for reporting on behalf of an Alternative Investment Fund (AIF) in accordance with Article 9(1c) of that Regulation, the unique code identifying that AIFM. Where the trustees or managers of an occupational pension scheme are responsible, and legally liable, for reporting on its behalf in accordance with Article 9(1d) of that Regulation, the unique code identifying those trustees on managers. This field is applicable only to the OTC derivatives."]
    extract
        if IsRegulatedMarketOrThirdCountryEquivalentMarket(
                    reportableInformation,
                    RegimeNameEnum -> UKEMIR
                ) = False // Not executed in a regulated market or 3rd country equivalent Market
        then ExtractPartyResponsibleForReportingIdentifier(reportingSide)
        as "3.3 Entity responsible for reporting"

reporting rule Counterparty1 from CollateralReportInstruction: <"Counterparty 1 (Reporting counterparty)">
    [regulatoryReference FCA UKEMIR table "3" dataElement "4" field "Counterparty 1"
        provision "Identifier of the counterparty to a derivative transaction who is fulfilling its reporting obligation via the report in question. In the case of an allocated derivative transaction executed by a fund manager on behalf of a fund, the fund and not the fund manager is reported as the counterparty."]
    extract reportingSide -> reportingParty
    then extract partyId -> identifier only-element
        as "3.4 Counterparty 1 (Reporting counterparty)"

reporting rule Counterparty2IdentifierType from CollateralReportInstruction: <"Counterparty 2 identifier type">
    [regulatoryReference FCA UKEMIR Margin table "3" dataElement "5" field "Counterparty 2 identifier type"
        provision "Indicator of whether LEI was used to identify counterparty 2."]
    extract reportingSide -> reportingCounterparty
    then extract
        if partyId -> identifierType only-element = PartyIdentifierTypeEnum -> LEI
        then True
        else False
        as "3.5 Counterparty 2 identifier type"

reporting rule Counterparty2 from CollateralReportInstruction: <"Counterparty 2">
    [regulatoryReference FCA UKEMIR table "3" dataElement "6" field "Counterparty 2"
        provision "Identifier of the second counterparty to a derivative transaction. In the case of an allocated derivative transaction executed by a fund manager on behalf of a fund, the fund and not the fund manager is reported as the counterparty."]
    extract reportingSide -> reportingCounterparty
    then extract partyId -> identifier only-element
        as "3.6 Counterparty 2"

reporting rule CollateralTimestamp from CollateralReportInstruction: <"Collateral Timestamp">
    [regulatoryReference FCA UKEMIR Margin table "3" dataElement "7" field "Collateral Timestamp"
        provision "Date and time as of which the values of the margins are reported."]
    extract collateralDetails -> collateralTimestamp
        as "3.7 Collateral timestamp"

reporting rule CollateralPortfolioIndicator from CollateralReportInstruction: <"Collateral Portfolio Indicator">
    [regulatoryReference FCA UKEMIR Margin table "3" dataElement "8" field "Collateral Portfolio Indicator"
        provision "Indicator of whether the collateralisation was performed on a portfolio basis. By 'on a portfolio basis', it is meant a set of transactions that are margined together (either on a net or a gross basis) contrary to the scenario where the margin is calculated and posted for each individual transaction separately."]
    extract collateralDetails -> collateral -> portfolioIdentifier exists
        as "3.8 Collateral portfolio indicator"

reporting rule CollateralPortfolioCode from CollateralReportInstruction: <"Collateral Portfolio Code">
    [regulatoryReference FCA UKEMIR Margin table "3" dataElement "9" field "Collateral portfolio code"
        provision "If collateral is reported on a portfolio basis, unique code assigned by counterparty 1 to the portfolio. This data element is not applicable if the collateralisation was performed on a transaction level basis, or if there is no collateral agreement or if no collateral is posted or received."]
    extract
        collateralDetails -> collateral -> portfolioIdentifier -> assignedIdentifier -> identifier
    // which filter do we apply to portfolioIdentifier and assignedIdentifier?
    then last // cardinality mismatch - see DRR ISSUE-624
        as "3.9 Collateral portfolio code"

reporting rule UTI from CollateralReportInstruction: <"UTI">
    [regulatoryReference FCA UKEMIR Margin table "3" dataElement "10" field "UTI"
        provision "Unique Transaction Identifier as referred to in Article 8 of the EMIR Technical Standards on the Standards, Formats, Frequency and Methods and Arrangements for Reporting 2023."]
    extract
        item -> collateralDetails -> uniqueTradeIdentifier -> assignedIdentifier -> identifier only-element
        as "3.10 UTI"

reporting rule CollateralisationCategory from CollateralReportInstruction: <"Collateralisation category">
    [regulatoryReference FCA UKEMIR Margin table "3" dataElement "11" field "Collateralisation category"
        provision "Indicate whether a collateral agreement between the counterparties exists. This data element is provided for each transaction or each portfolio, depending on whether the collateralisation is performed at the transaction or portfolio level, and is applicable to both cleared and uncleared transactions."]
    extract
        item -> collateralDetails -> collateralisationCategory to-enum CollateralisationType3Code__1
        as "3.11 Collateralisation category"

reporting rule InitialMarginPostedByTheCounterparty1PreHaircut from CollateralReportInstruction: <"Initial margin posted by the counterparty 1 (pre-haircut)">
    [regulatoryReference FCA UKEMIR Margin table "3" dataElement "12" field "Initial margin posted by the counterparty 1 (pre-haircut)"
        provision "Monetary value of initial margin that has been posted by counterparty 1, including any margin that is in transit and pending settlement. If the collateralisation is performed at portfolio level, the initial margin posted relates to the whole portfolio; if the collateralisation is performed for single transactions, the initial margin posted relates to such single transaction. This refers to the total current value of the initial margin, rather than to its daily change. The data element refers both to uncleared and centrally cleared transactions. For centrally cleared transactions, the data element does not include default fund contributions, nor collateral posted against liquidity provisions to the CCP, i.e., committed credit lines. If the initial margin posted is denominated in more than one currency, those amounts are converted into a single currency chosen by counterparty 1 and reported as one total value."]
    extract
        cde.collateral.InitialMarginPostedByReportingCounterpartyPreHaircut(
                item,
                reportingSide -> reportingParty
            )
        as "3.12 Initial margin posted by the counterparty 1 (pre-haircut)"

reporting rule InitialMarginPostedByTheCounterparty1PostHaircut from CollateralReportInstruction: <"Initial margin posted by the counterparty 1 (post-haircut)">
    [regulatoryReference FCA UKEMIR Margin table "3" dataElement "16" field "Initial margin posted by the counterparty 1 (post-haircut)"
        provision "Monetary value of initial margin that has been posted by counterparty 1, including any margin that is in transit and pending settlement. If the collateralisation is performed at portfolio level, the initial margin posted relates to the whole portfolio; if the collateralisation is performed for single transactions, the initial margin posted relates to such single transaction. This refers to the total current value of the initial margin after application of the haircut (if applicable), rather than to its daily change.The data element refers both to uncleared and centrally cleared transactions. For centrally cleared transactions, the data element does not include default fund contributions, nor collateral posted against liquidity provisions to the central counterparty, i.e. committed credit lines. If the initial margin posted is denominated in more than one currency, those amounts are converted into a single currency chosen by counterparty 1 and reported as one total value."]
    extract
        cde.collateral.InitialMarginPostedByReportingCounterpartyPostHaircut(
                item,
                reportingSide -> reportingParty
            )
        as "3.13 Initial margin posted by the counterparty 1 (post-haircut)"

reporting rule InitialMarginPostedByCounterparty1Currency from CollateralReportInstruction: <"Currency of the initial margins posted">
    [regulatoryReference FCA UKEMIR Margin table "3" dataElement "17" field "Currency of the variation margins posted"
        provision "Currency in which the initial margin posted is denominated. If the initial margin posted is denominated in more than one currency, this data element reflects one of those currencies into which counterparty 1 has chosen to convert all the values of posted initial margins."]
    extract
        cde.collateral.InitialMarginPostedByReportingCounterpartyCurrency(
                item,
                reportingSide -> reportingParty
            )
        as "3.14 Currency of the initial margins posted"

reporting rule VariationMarginPostedByTheCounterparty1PreHaircut from CollateralReportInstruction: <"Variation margin posted by the counterparty 1 (pre-haircut)">
    [regulatoryReference FCA UKEMIR Margin table "3" dataElement "15" field "Variation margin posted by the counterparty 1 (pre-haircut)"
        provision "Monetary value of the variation margin posted by counterparty 1 including the cash-settled margin, and any margin that is in transit and pending settlement. Contingent variation margin is not included. If the collateralisation is performed at portfolio level, the variation margin posted relates to the whole portfolio; if the collateralisation is performed for single transactions, the variation margin posted relates to such single transaction.This field refers to the total current value of the variation margin, cumulated since the first reporting of variation margins posted for the portfolio/transaction. If the variation margin posted is denominated in more than one currency, those amounts are converted into a single currency chosen by counterparty 1 and reported as one total value."]
    extract
        cde.collateral.VariationMarginPostedByReportingCounterpartyPreHaircut(
                item,
                reportingSide -> reportingParty
            )
        as "3.15 Variation margin posted by the counterparty 1 (pre-haircut)"

reporting rule VariationMarginPostedByTheCounterparty1PostHaircut from CollateralReportInstruction: <"Variation margin posted by the counterparty 1 (post-haircut)">
    [regulatoryReference FCA UKEMIR Margin table "3" dataElement "16" field "Variation margin posted by the counterparty 1 (post-haircut)"
        provision "Monetary value of the variation margin posted by counterparty 1 including the cash-settled margin, and any margin that is in transit and pending settlement. Contingent variation margin is not included. If the collateralisation is performed at portfolio level, the variation margin posted relates to the whole portfolio; if the collateralisation is performed for single transactions, the variation margin posted relates to such single transaction. This data element refers to the total current value of the variation margin after application of the haircut (if applicable), cumulated since the first reporting of posted variation margins for the portfolio /transaction. If the variation margin posted is denominated in more than one currency, those amounts are converted into a single currency chosen by counterparty 1 and reported as one total value."]
    extract
        cde.collateral.VariationMarginPostedByReportingCounterpartyPostHaircut(
                item,
                reportingSide -> reportingParty
            )
        as "3.16 Variation margin posted by the counterparty 1 (post-haircut)"

reporting rule VariationMarginPostedByCounterparty1Currency from CollateralReportInstruction: <"Currency of the variation margins posted">
    [regulatoryReference FCA UKEMIR Margin table "3" dataElement "17" field "Currency of the variation margins posted"
        provision "Currency in which the variation margin posted is denominated. If the variation margin posted is denominated in more than one currency, this data element reflects one of those currencies into which counterparty 1 has chosen to convert all the values of posted variation margins."]
    extract
        cde.collateral.VariationMarginPostedByReportingCounterpartyCurrency(
                item,
                reportingSide -> reportingParty
            )
        as "3.17 Currency of the variation margins posted"

reporting rule ExcessCollateralPostedByTheCounterparty1 from CollateralReportInstruction: <"Excess Collateral Posted By The Counterparty 1">
    [regulatoryReference FCA UKEMIR Margin table "3" dataElement "18" field "Excess collateral posted by the counterparty 1"
        provision "Monetary value of any additional collateral posted by counterparty 1 separate and independent from initial and variation margin. This field refers to the total current value of the excess collateral before application of the haircut, if applicable, rather than to its daily change. Any initial or variation margin amount posted that exceeds the required initial margin or required variation margin, is reported as part of the initial margin posted or variation margin posted respectively rather than included as excess collateral posted. For centrally cleared transactions, excess collateral is reported only to the extent it can be assigned to a specific portfolio or transaction."]
    [regulatoryReference ISDA EMEADataAndReporting date "20230206"
        provision "No scenarios identified, this field will not be populated until further guidance from the ROC and/or ESMA."]
    cde.collateral.ExcessCollateralPostedByTheReportingCounterparty
        as "3.18 Excess collateral posted by the counterparty 1"

reporting rule CurrencyOfTheExcessCollateralPosted from CollateralReportInstruction: <"Currency Of The Excess Collateral Posted">
    [regulatoryReference FCA UKEMIR Margin table "3" dataElement "19" field "Currency of the excess collateral posted"
        provision "Currency in which the excess collateral posted is denominated. If the excess collateral posted is denominated in more than one currency, this field reflects one of those currencies into which counterparty 1 has chosen to convert all the values of posted excess collateral."]
    [regulatoryReference ISDA EMEADataAndReporting date "20230206"
        provision "No scenarios identified, this field will not be populated until further guidance from the ROC and/or ESMA."]
    cde.collateral.CurrencyOfExcessCollateralPosted
        as "3.19 Currency of the excess collateral posted"

reporting rule InitialMarginCollectedByCounterparty1PreHaircut from CollateralReportInstruction: <"Initial margin collected by the counterparty 1 (pre-haircut)">
    [regulatoryReference FCA UKEMIR Margin table "3" dataElement "20" field "Initial margin collected by the counterparty 1 (pre-haircut)"
        provision "Monetary value of initial margin that has been collected by counterparty 1, including any margin that is in transit and pending settlement. If the collateralisation is performed at portfolio level, the initial margin collected relates to the whole portfolio; if the collateralisation is performed for single transactions, the initial margin collected relates to such single transaction. This refers to the total current value of the initial margin, rather than to its daily change. The data element refers both to uncleared and centrally cleared transactions. For centrally cleared transactions, the data element does not include collateral collected by the central counterparty as part of its investment activity. If the initial margin collected is denominated in more than one currency, those amounts are converted into a single currency chosen by counterparty 1 and reported as one total value."]
    extract
        cde.collateral.InitialMarginCollectedByReportingCounterpartyPreHaircut(
                item,
                reportingSide -> reportingParty
            )
        as "3.20 Initial margin collected by the counterparty 1 (pre-haircut)"

reporting rule InitialMarginCollectedByCounterparty1PostHaircut from CollateralReportInstruction: <"Initial margin collected by the counterparty 1 (post-haircut)">
    [regulatoryReference FCA UKEMIR Margin table "3" dataElement "21" field "Initial margin collected by the counterparty 1 (post-haircut)"
        provision "Monetary value of initial margin that has been collected by counterparty 1, including any margin that is in transit and pending settlement. If the collateralisation is performed at portfolio level, the initial margin collected relates to the whole portfolio; if the collateralisation is performed for single transactions, the initial margin collected relates to such single transaction. This refers to the total current value of the initial margin after application of the haircut (if applicable), rather than to its daily change. The data element refers both to uncleared and centrally cleared transactions. For centrally cleared transactions, the data element does not include collateral collected by the central counterparty as part of its investment activity. If the initial margin collected is denominated in more than one currency, those amounts are converted into a single currency chosen by counterparty 1 and reported as one total value."]
    cde.collateral.InitialMarginCollectedByReportingCounterpartyPostHaircut(
            item,
            reportingSide -> reportingParty
        )
        as "3.21 Initial margin collected by the counterparty 1 (post-haircut)"

reporting rule InitialMarginCollectedByCounterparty1Currency from CollateralReportInstruction: <"Currency of initial margin collected">
    [regulatoryReference FCA UKEMIR Margin table "3" dataElement "22" field "Currency of initial margin collected"
        provision "Currency in which the initial margin collected is denominated. If the initial margin collected is denominated in more than one currency, this data element reflects one of those currencies into which counterparty 1 has chosen to convert all the values of collected initial margins."]
    extract
        cde.collateral.InitialMarginCollectedByReportingCounterpartyCurrency(
                item,
                reportingSide -> reportingParty
            )
        as "3.22 Currency of initial margin collected"

reporting rule VariationMarginCollectedByTheCounterparty1PreHaircut from CollateralReportInstruction: <"Variation margin collected by the counterparty 1 (pre-haircut)">
    [regulatoryReference FCA UKEMIR Margin table "3" dataElement "23" field "Variation margin collected by the counterparty 1 (pre-haircut)"
        provision "Monetary value of the variation margin collected by the counterparty 1 (including the cash-settled one), and including any margin that is in transit and pending settlement.Contingent variation margin is not included. If the collateralisation is performed at portfolio level, the variation margin collected relates to the whole portfolio; if the collateralisation is performed for single transactions, the variation margin collected relates to such single transaction. This refers to the total current value of the variation margin, cumulated since the first reporting of collected variation margins for the portfolio/transaction. If the variation margin collected is denominated in more than one currency, those amounts are converted into a single currency chosen by the counterparty 1 and reported as one total value."]
    extract
        cde.collateral.VariationMarginCollectedByReportingCounterpartyPreHaircut(
                item,
                reportingSide -> reportingParty
            )
        as "3.23 Variation margin collected by the counterparty 1 (pre-haircut)"

reporting rule VariationMarginCollectedByTheCounterparty1PostHaircut from CollateralReportInstruction: <"Variation margin collected by the counterparty 1 (post-haircut)">
    [regulatoryReference FCA UKEMIR Margin table "3" dataElement "24" field "Variation margin collected by the counterparty 1 (post-haircut)"
        provision "Monetary value of the variation margin collected by counterparty 1 including the cash-settled margin,  and any margin that is in transit and pending settlement. Contingent variation margin is not included. If the collateralisation is performed at portfolio level, the variation margin collected relates to the whole portfolio; if the collateralisation is performed for single transactions, the variation margin collected relates to such single transaction. This field refers to the total current value of the variation margin collected after application of the haircut, if applicable, cumulated since the first reporting of collected variation margins for the portfolio or transaction. If the variation margin collected is denominated in more than one currency, those amounts are converted into a single currency chosen by counterparty 1 and reported as one total value."]
    extract
        cde.collateral.VariationMarginCollectedByReportingCounterpartyPostHaircut(
                item,
                reportingSide -> reportingParty
            )
        as "3.24 Variation margin collected by the counterparty 1 (post-haircut)"

reporting rule VariationMarginCollectedByCounterparty1Currency from CollateralReportInstruction: <"Currency of variation margin collected">
    [regulatoryReference FCA UKEMIR Margin table "3" dataElement "25" field "Currency of variation margin collected"
        provision "Currency in which the variation margin collected is denominated. If the variation margin collected is denominated in more than one currency, this data element reflects one of those currencies into which counterparty 1 has chosen to convert all the values of collected variation margins."]
    extract
        cde.collateral.VariationMarginCollectedByReportingCounterpartyCurrency(
                item,
                reportingSide -> reportingParty
            )
        as "3.25 Currency of variation margin collected"

reporting rule ExcessCollateralCollectedByTheCounterparty1 from CollateralReportInstruction: <"Excess Collateral Collected By The Counterparty 1">
    [regulatoryReference FCA UKEMIR Margin table "3" dataElement "26" field "Excess collateral collected by the counterparty 1"
        provision "Monetary value of any additional collateral collected by counterparty 1 separate and independent from initial and variation margin. This data element refers to the total current value of the excess collateral before application of the haircut, if applicable, rather than to its daily change. Any initial or variation margin amount collected that exceeds the required initial margin or required variation margin, is reported as part of the initial margin collected or variation margin collected respectively, rather than included as excess collateral collected. For centrally cleared transactions excess collateral is reported only to the extent it can be assigned to a specific portfolio or transaction."]
    [regulatoryReference ISDA EMEADataAndReporting date "20230206"
        provision "No scenarios identified, this field will not be populated until further guidance from the ROC and/or ESMA."]
    cde.collateral.ExcessCollateralCollectedByTheReportingCounterparty
        as "3.26 Excess collateral collected by the counterparty 1"

reporting rule CurrencyOfExcessCollateralCollected from CollateralReportInstruction: <"Currency Of Excess Collateral Collected">
    [regulatoryReference FCA UKEMIR Margin table "3" dataElement "27" field "Currency of excess collateral collected"
        provision "Currency in which the excess collateral collected is denominated. If the excess collateral is denominated in more than one currency, this data element reflects one of those currencies into which counterparty 1 has chosen to convert all the values of collected excess collateral."]
    [regulatoryReference ISDA EMEADataAndReporting date "20230206"
        provision "No scenarios identified, this field will not be populated until further guidance from the ROC and/or ESMA."]
    cde.collateral.CurrencyOfExcessCollateralCollected
        as "3.27 Currency of excess collateral collected"

reporting rule ActionType from CollateralReportInstruction: <"Action Type">
    [regulatoryReference FCA UKEMIR Margin table "3" dataElement "28" field "Action type"
        provision "The report shall contain one of the following action types: (a) a new margin balance or a modification of the details of the margins shall be identified as Margin update; (b) a correction of data fields that were submitted incorrectly in a previous report shall be identified as Correction."]
    extract collateralDetails -> action
        as "3.28 Action Type"

reporting rule EventDate from CollateralReportInstruction: <"Event date">
    [regulatoryReference FCA UKEMIR Margin table "3" dataElement "29" field "Event date"
        provision "Date on which the reportable event relating to the derivative contract and captured by the report took place. In the case of collateral update - the date for which the information contained in the report is provided."]
    // To do
    extract ExtractDateFromDateTime(collateralDetails -> collateralTimestamp)
        as "3.29 Event date"

reporting rule MICCollateral from CollateralReportInstruction:
    GetVenueOfExecution(reportableInformation)
