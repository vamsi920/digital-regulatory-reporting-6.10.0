namespace drr.projection.iso20022.jfsa.rewrite.margin
version "${project.version}"

import drr.regulation.common.*
import drr.regulation.jfsa.rewrite.margin.*

import iso20022.auth108.jfsa.*

func Project_JFSARewriteMarginReportToIso20022:
    [projection XML]
    inputs:
        marginReport JFSAMarginReport (1..1)
    output:
        iso20022Report Document (1..1)

    set iso20022Report -> derivsTradMrgnDataRpt -> tradData -> rpt:
        TradeReport34Choice__1 {
            mrgnUpd: if marginReport -> collateral -> actionType = MarginActionEnum -> MARU
                then Create_MarginReportData(
                        marginReport -> partiesToTheDerivative,
                        marginReport -> collateral
                    )
        }

func Create_MarginReportData:
    inputs:
        partiesToTheDerivative PartiesToTheDerivative (1..1)
        collateralReport CollateralReport (1..1)
    output:
        details MarginReportData9__1 (1..1)

    set details:
        MarginReportData9__1 {
            rptgTmStmp: partiesToTheDerivative -> reportingTimestamp,
            txId: UniqueTransactionIdentifier2Choice__1 {
                unqTxIdr: if IsMax32UpperCaseAlphanumericText(collateralReport -> uti)
                    then collateralReport -> uti,
                prtry: if IsMax32UpperCaseAlphanumericText(collateralReport -> uti) = False
                    then GenericIdentification175__1 {
                        id: collateralReport -> uti
            }},
            ctrPtyId: Create_CounterpartySpecificData(
                    partiesToTheDerivative,
                    collateralReport
                ),
            coll: MarginCollateralReport5__1 {
                collstnCtgy: collateralReport -> collateralisationCategory to-enum CollateralisationType3Code,
                collPrtflCd: CollateralPortfolioCode6Choice__1 {
                    mrgnPrtflCd: MarginPortfolio4__1 {
                        initlMrgnPrtflCd: PortfolioCode5Choice__1 {
                            prtfl: PortfolioIdentification3__1 {
                                cd: if collateralReport -> initialMarginCollateralPortfolioCode exists
                                    then collateralReport -> initialMarginCollateralPortfolioCode
                            },
                            noPrtfl: if collateralReport -> initialMarginCollateralPortfolioCode is absent
                                then NotApplicable1Code -> NOAP
                        },
                        vartnMrgnPrtflCd: PortfolioCode5Choice__1 {
                            prtfl: PortfolioIdentification3__1 {
                                cd: if collateralReport -> variationMarginCollateralPortfolioCode exists
                                    then collateralReport -> variationMarginCollateralPortfolioCode
                            },
                            noPrtfl: if collateralReport -> variationMarginCollateralPortfolioCode is absent
                                then NotApplicable1Code -> NOAP
                }}},
                tmStmp: collateralReport -> eventTimestamp
            },
            pstdMrgnOrColl: Create_PostedMarginReport(collateralReport),
            rcvdMrgnOrColl: Create_ReceivedMarginReport(collateralReport),
            techAttrbts: TechnicalAttributes6__1 {
                techRcrdId: collateralReport -> technicalRecordId + "_" + partiesToTheDerivative -> reportingTimestamp to-string
        }}

func Create_CounterpartySpecificData:
    inputs:
        partiesToTheDerivative PartiesToTheDerivative (1..1)
        collateralReport CollateralReport (1..1)
    output:
        ctrPtySpcfcData TradeCounterpartyReport20__1 (1..1)

    set ctrPtySpcfcData:
        TradeCounterpartyReport20__1 {
            rptgCtrPty: Counterparty45__1 {
                id: PartyIdentification248Choice__1 {
                    lgl: LegalPersonIdentification1__1 {
                        id: OrganisationIdentification15Choice__1 {
                            lei: partiesToTheDerivative -> counterparty1
            }}}},
            submitgAgt: OrganisationIdentification15Choice__1 {
                lei: partiesToTheDerivative -> submitterIdentifier
            },
            othrCtrPty: Counterparty46__1 {
                idTp: PartyIdentification248Choice__2 {
                    lgl: if partiesToTheDerivative -> counterparty2IdentifierType = True
                        then LegalPersonIdentification1__2 {
                            id: OrganisationIdentification15Choice__2 {
                                lei: partiesToTheDerivative -> counterparty2,
                                ...
                    }},
                    ntrl: if partiesToTheDerivative -> counterparty2IdentifierType = False
                        then NaturalPersonIdentification3__1 {
                            id: NaturalPersonIdentification2__1 {
                                id: GenericIdentification175__1 {
                                    id: partiesToTheDerivative -> counterparty2
            }}}}},
            nttyRspnsblForRpt: OrganisationIdentification15Choice__1 {
                lei: partiesToTheDerivative -> entityResponsibleForReporting
        }}

func Create_PostedMarginReport:
    inputs:
        collateralReport CollateralReport (1..1)
    output:
        postMarginData PostedMarginOrCollateral6__1 (1..1)

    set postMarginData:
        PostedMarginOrCollateral6__1 {
            initlMrgnPstdPreHrcut: ActiveOrHistoricCurrencyAnd5DecimalAmount {
                value: collateralReport -> initialMarginPostedByTheReportingCounterpartyPreHaircut,
                ccy: collateralReport -> currencyofInitialMarginPosted to-string
            },
            initlMrgnPstdPstHrcut: ActiveOrHistoricCurrencyAnd5DecimalAmount {
                value: collateralReport -> initialMarginPostedByTheReportingCounterpartyPostHaircut,
                ccy: collateralReport -> currencyofInitialMarginPosted to-string
            },
            vartnMrgnPstdPreHrcut: ActiveOrHistoricCurrencyAnd5DecimalAmount {
                value: collateralReport -> variationMarginPostedByTheReportingCounterpartyPreHaircut,
                ccy: collateralReport -> currencyOfVariationMarginPosted to-string
            },
            vartnMrgnPstdPstHrcut: ActiveOrHistoricCurrencyAnd5DecimalAmount {
                value: collateralReport -> variationMarginPostedByTheReportingCounterpartyPostHaircut,
                ccy: collateralReport -> currencyOfVariationMarginPosted to-string
            },
            xcssCollPstd: ActiveOrHistoricCurrencyAnd5DecimalAmount {
                value: collateralReport -> excessCollateralPostedByTheReportingCounterparty,
                ccy: collateralReport -> currencyOfTheExcessCollateralPosted to-string
        }}

func Create_ReceivedMarginReport:
    inputs:
        collateralReport CollateralReport (1..1)
    output:
        rcvdMarginData ReceivedMarginOrCollateral6__1 (1..1)

    set rcvdMarginData:
        ReceivedMarginOrCollateral6__1 {
            initlMrgnRcvdPreHrcut: ActiveOrHistoricCurrencyAnd5DecimalAmount {
                value: collateralReport -> initialMarginCollectedByReportingCounterpartyPreHaircut,
                ccy: collateralReport -> currencyofInitialMarginPosted to-string
            },
            initlMrgnRcvdPstHrcut: ActiveOrHistoricCurrencyAnd5DecimalAmount {
                value: collateralReport -> initialMarginCollectedByReportingCounterpartyPostHaircut,
                ccy: collateralReport -> currencyofInitialMarginPosted to-string
            },
            vartnMrgnRcvdPreHrcut: ActiveOrHistoricCurrencyAnd5DecimalAmount {
                value: collateralReport -> variationMarginCollectedByTheReportingCounterpartyPreHaircut,
                ccy: collateralReport -> currencyOfVariationMarginPosted to-string
            },
            vartnMrgnRcvdPstHrcut: ActiveOrHistoricCurrencyAnd5DecimalAmount {
                value: collateralReport -> variationMarginCollectedByTheReportingCounterpartyPostHaircut,
                ccy: collateralReport -> currencyOfVariationMarginPosted to-string
            },
            xcssCollRcvd: ActiveOrHistoricCurrencyAnd5DecimalAmount {
                value: collateralReport -> excessCollateralCollectedByTheReportingCounterparty,
                ccy: collateralReport -> currencyOfTheExcessCollateralCollected to-string
        }}
