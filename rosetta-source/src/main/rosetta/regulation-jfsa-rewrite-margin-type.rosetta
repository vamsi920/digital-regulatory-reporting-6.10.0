namespace drr.regulation.jfsa.rewrite.margin
version "${project.version}"

import cdm.base.*
import cdm.base.staticdata.asset.common.*

import drr.regulation.common.*
import drr.regulation.jfsa.*
import drr.standards.iso.*

type JFSAMarginReport:
    partiesToTheDerivative PartiesToTheDerivative (1..1)
    collateral CollateralReport (1..1)

    condition DTCC_JFSA_BR_0004_03: <"Reporting timestamp Condition 3">
        [docReference JFSA DTCC_Specs dataElement "4" field "Reporting timestamp"
          provision "[Reporting timestamp] must be equal to or before the [Message Received timestamp] derived by the TR. The date value of the [Reporting timestamp] must NOT be more than 2 days before the date value of the [Message Received timestamp]."]
        // This field is set to True as this data is known post submission.
        if collateral -> actionType = MarginActionEnum -> MARU
        then (True)

    condition DTCC_JFSA_BR_0007_01: <"Counterparty 1 Condition">
        [docReference JFSA DTCC_Specs dataElement "7" field "Counterparty 1"
          provision "[Counterparty 1] must NOT = [Counterparty 2] if [Counterparty 2 identifier type] is = LEI"]
        if collateral -> actionType = MarginActionEnum -> MARU
        then (if partiesToTheDerivative -> counterparty2IdentifierType = True then partiesToTheDerivative -> counterparty1 <> partiesToTheDerivative -> counterparty2)

    condition DTCC_JFSA_BR_0008_01: <"Counterparty 2 Condition">
        [docReference JFSA DTCC_Specs dataElement "8" field "Counterparty 2"
          provision "[Counterparty 1] must NOT = [Counterparty 2] if [Counterparty 2 identifier type] is = LEI"]
        if collateral -> actionType = MarginActionEnum -> MARU
        then (if partiesToTheDerivative -> counterparty2IdentifierType = True then partiesToTheDerivative -> counterparty2 <> partiesToTheDerivative -> counterparty1)

    condition DTCC_JFSA_BR_0025_01: <"Unique transaction identifier (UTI) Condition">
        [docReference JFSA DTCC_Specs dataElement "25" field "Unique transaction identifier (UTI)"
          provision "Required if [Initial margin collateral portfolio code] = 'NOAP' and [Variation margin collateral portfolio code] is = 'NOAP' or blank."]
        if collateral -> actionType = MarginActionEnum -> MARU
        then (if collateral -> initialMarginCollateralPortfolioCode = "NOAP" and collateral -> initialMarginCollateralPortfolioCode = "NOAP" or collateral -> initialMarginCollateralPortfolioCode is absent then collateral -> uti exists)

    condition DTCC_JFSA_BR_0045_01: <"Initial margin posted by the reporting counterparty (pre-haircut) Condition">
        [docReference JFSA DTCC_Specs dataElement "45" field "Initial margin posted by the reporting counterparty (pre-haircut)"
          provision "Required if [Collateralisation category] = 'OWC1' or 'OWP1' or 'FLCL'."]
        if collateral -> actionType = MarginActionEnum -> MARU
        then (if [CollateralisationType3Code__1 -> OWC1, CollateralisationType3Code__1 -> OWP1, CollateralisationType3Code__1 -> FLCL] any = collateral -> collateralisationCategory then collateral -> initialMarginPostedByTheReportingCounterpartyPreHaircut exists)

    condition DTCC_JFSA_BR_0046_01: <"Initial margin posted by the reporting counterparty (post-haircut) Condition">
        [docReference JFSA DTCC_Specs dataElement "46" field "Initial margin posted by the reporting counterparty (post-haircut)"
          provision "Required if [Collateralisation category] = 'OWC1' or 'OWP1' or 'FLCL'."]
        if collateral -> actionType = MarginActionEnum -> MARU
        then (if [CollateralisationType3Code__1 -> OWC1, CollateralisationType3Code__1 -> OWP1, CollateralisationType3Code__1 -> FLCL] any = collateral -> collateralisationCategory then collateral -> initialMarginPostedByTheReportingCounterpartyPostHaircut exists)

    condition DTCC_JFSA_BR_0047_01: <"Currency of initial margin posted Condition">
        [docReference JFSA DTCC_Specs dataElement "47" field "Currency of initial margin posted"
          provision "Required if [Initial margin posted by the reporting counterparty (pre-haircut)] is populated. Required if [Initial margin posted by the reporting counterparty (post-haircut)] is populated."]
        if collateral -> actionType = MarginActionEnum -> MARU
        then (if collateral -> initialMarginPostedByTheReportingCounterpartyPreHaircut exists or collateral -> initialMarginPostedByTheReportingCounterpartyPostHaircut exists then collateral -> currencyofInitialMarginPosted exists)

    condition DTCC_JFSA_BR_0048_01: <"Initial margin collected by the reporting counterparty (pre-haircut) Condition">
        [docReference JFSA DTCC_Specs dataElement "48" field "Initial margin collected by the reporting counterparty (pre-haircut)"
          provision "Required if [Collateralisation category] = OWC2 or OWP2 or FLCL."]
        if collateral -> actionType = MarginActionEnum -> MARU
        then (if [CollateralisationType3Code__1 -> OWC2, CollateralisationType3Code__1 -> OWP2, CollateralisationType3Code__1 -> FLCL] any = collateral -> collateralisationCategory then collateral -> initialMarginCollectedByReportingCounterpartyPreHaircut exists)

    condition DTCC_JFSA_BR_0049_01: <"Initial margin collected by the reporting counterparty (post-haircut) Condition">
        [docReference JFSA DTCC_Specs dataElement "49" field "Initial margin collected by the reporting counterparty (post-haircut)"
          provision "Required if [Collateralisation category] = OWC2 or OWP2 or FLCL."]
        if collateral -> actionType = MarginActionEnum -> MARU
        then (if [CollateralisationType3Code__1 -> OWC2, CollateralisationType3Code__1 -> OWP2, CollateralisationType3Code__1 -> FLCL] any = collateral -> collateralisationCategory then collateral -> initialMarginCollectedByReportingCounterpartyPostHaircut exists)

    condition DTCC_JFSA_BR_0050_01: <"Currency of initial margin collected Condition">
        [docReference JFSA DTCC_Specs dataElement "50" field "Currency of initial margin collected"
          provision "Required if [Initial margin collected by the reporting counterparty (pre-haircut)] is populated. Required if [Initial margin collected by the reporting counterparty (post-haircut)] is populated."]
        if collateral -> actionType = MarginActionEnum -> MARU
        then (if collateral -> initialMarginCollectedByReportingCounterpartyPreHaircut exists or collateral -> initialMarginCollectedByReportingCounterpartyPostHaircut exists then collateral -> currencyofInitialMarginCollected exists)

    condition DTCC_JFSA_BR_0051_01: <"Variation margin posted by the reporting counterparty (pre-haircut) Condition">
        [docReference JFSA DTCC_Specs dataElement "51" field "Variation margin posted by the reporting counterparty (pre-haircut)"
          provision "Required if [Collateralisation category] = 'PRC1' or 'PRCL' or 'OWC1' or 'OWP1' or 'OWP2' or 'FLCL'."]
        if collateral -> actionType = MarginActionEnum -> MARU
        then (if [CollateralisationType3Code__1 -> PRC1, CollateralisationType3Code__1 -> PRCL, CollateralisationType3Code__1 -> OWC1, CollateralisationType3Code__1 -> OWP1, CollateralisationType3Code__1 -> OWP2, CollateralisationType3Code__1 -> FLCL] any = collateral -> collateralisationCategory then collateral -> initialMarginPostedByTheReportingCounterpartyPreHaircut exists)

    condition DTCC_JFSA_BR_0052_01: <"Variation margin posted by the reporting counterparty (post-haircut) Condition">
        [docReference JFSA DTCC_Specs dataElement "52" field "Variation margin posted by the reporting counterparty (post-haircut)"
          provision "Required if [Collateralisation category] = 'PRC1' or 'PRCL' or 'OWC1' or 'OWP1' or 'OWP2' or 'FLCL'."]
        if collateral -> actionType = MarginActionEnum -> MARU
        then (if [CollateralisationType3Code__1 -> PRC1, CollateralisationType3Code__1 -> PRCL, CollateralisationType3Code__1 -> OWC1, CollateralisationType3Code__1 -> OWP1, CollateralisationType3Code__1 -> OWP2, CollateralisationType3Code__1 -> FLCL] any = collateral -> collateralisationCategory then collateral -> initialMarginPostedByTheReportingCounterpartyPostHaircut exists)

    condition DTCC_JFSA_BR_0053_01: <"Currency of variation margin posted Condition">
        [docReference JFSA DTCC_Specs dataElement "53" field "Currency of variation margin posted"
          provision "Required if [Variation margin posted by the reporting counterparty (pre-haircut)] is populated. Required if [Variation margin posted by the reporting counterparty (post-haircut)] is populated."]
        if collateral -> actionType = MarginActionEnum -> MARU
        then (if collateral -> variationMarginPostedByTheReportingCounterpartyPreHaircut exists or collateral -> variationMarginPostedByTheReportingCounterpartyPostHaircut exists then collateral -> currencyOfVariationMarginPosted exists)

    condition DTCC_JFSA_BR_0054_01: <"Variation margin collected by the reporting counterparty (pre-haircut) Condition">
        [docReference JFSA DTCC_Specs dataElement "54" field "Variation margin collected by the reporting counterparty (pre-haircut)"
          provision "Required if [Collateralisation category] = 'PRC2' or 'PRCL' or 'OWC2' or 'OWP1' or 'OWP2' or 'FLCL'."]
        if collateral -> actionType = MarginActionEnum -> MARU
        then (if [CollateralisationType3Code__1 -> PRC2, CollateralisationType3Code__1 -> PRCL, CollateralisationType3Code__1 -> OWC2, CollateralisationType3Code__1 -> OWP1, CollateralisationType3Code__1 -> OWP2, CollateralisationType3Code__1 -> FLCL] any = collateral -> collateralisationCategory then collateral -> variationMarginCollectedByTheReportingCounterpartyPreHaircut exists)

    condition DTCC_JFSA_BR_0055_01: <"Variation margin collected by the reporting counterparty (post-haircut) Condition">
        [docReference JFSA DTCC_Specs dataElement "55" field "Variation margin collected by the reporting counterparty (post-haircut)"
          provision "Required if [Collateralisation category] = 'PRC2' or 'PRCL' or 'OWC2' or 'OWP1' or 'OWP2' or 'FLCL'."]
        if collateral -> actionType = MarginActionEnum -> MARU
        then (if [CollateralisationType3Code__1 -> PRC2, CollateralisationType3Code__1 -> PRCL, CollateralisationType3Code__1 -> OWC2, CollateralisationType3Code__1 -> OWP1, CollateralisationType3Code__1 -> OWP2, CollateralisationType3Code__1 -> FLCL] any = collateral -> collateralisationCategory then collateral -> variationMarginCollectedByTheReportingCounterpartyPostHaircut exists)

    condition DTCC_JFSA_BR_0056_01: <"Currency of variation margin collected Condition">
        [docReference JFSA DTCC_Specs dataElement "56" field "Currency of variation margin collected"
          provision "Required if [Variation margin collected by the reporting counterparty (pre-haircut)] is populated. Required if [Variation margin collected by the reporting counterparty (post-haircut)] is populated."]
        if collateral -> actionType = MarginActionEnum -> MARU
        then (if collateral -> variationMarginCollectedByTheReportingCounterpartyPreHaircut exists or collateral -> variationMarginCollectedByTheReportingCounterpartyPostHaircut exists then collateral -> currencyOfVariationMarginCollected exists)

    condition DTCC_JFSA_BR_0058_01: <"Currency of excess collateral posted Condition">
        [docReference JFSA DTCC_Specs dataElement "58" field "Currency of excess collateral posted"
          provision "Required if [Excess collateral posted by the reporting counterparty] is populated."]
        if collateral -> actionType = MarginActionEnum -> MARU
        then (if collateral -> excessCollateralPostedByTheReportingCounterparty exists then collateral -> currencyOfTheExcessCollateralPosted exists)

    condition DTCC_JFSA_BR_0060_01: <"Currency of excess collateral collected Condition">
        [docReference JFSA DTCC_Specs dataElement "60" field "Currency of excess collateral collected"
          provision "Required if [Excess collateral collected by the reporting counterparty] is populated."]
        if collateral -> actionType = MarginActionEnum -> MARU
        then (if collateral -> excessCollateralCollectedByTheReportingCounterparty exists then collateral -> currencyOfTheExcessCollateralCollected exists)

    condition DTCC_JFSA_BR_0062_02: <"Initial margin collateral portfolio code Condition 2">
        [docReference JFSA DTCC_Specs dataElement "62" field "Initial margin collateral portfolio code"
          provision "Reject if [UTI] is populated and [Initial margin collateral portfolio code] does NOT = 'NOAP'. Reject if the value [NOAP] is submitted in '/Prtfl/Cd'."]
        // [NOAP] value for '/Prtfl/Cd' = 'False' value for [Collateral portfolio indicator]
        if collateral -> actionType = MarginActionEnum -> MARU
        then (if collateral -> uti exists
                    and collateral -> initialMarginCollateralPortfolioCode <> "NOAP"
            then collateral -> initialMarginCollateralPortfolioCode is absent
            else if collateral -> collateralPortfolioIndicator = False
            then collateral -> variationMarginCollateralPortfolioCode is absent)

    condition DTCC_JFSA_BR_0063_03: <"Variation margin collateral portfolio code Condition 3">
        [docReference JFSA DTCC_Specs dataElement "63" field "Variation margin collateral portfolio code"
          provision "Required if [Initial margin collateral portfolio code] = 'NOAP' and [Unique transaction identifier (UTI)] is blank. Reject if the value [NOAP] is submitted in '/Prtfl/Cd'. Reject if [UTI] is populated and [Variation margin collateral portfolio code] does NOT = 'NOAP'."]
        // [NOAP] value for '/Prtfl/Cd' = 'False' value for [Collateral portfolio indicator]
        if collateral -> actionType = MarginActionEnum -> MARU
        then (if collateral -> uti is absent
                    and collateral -> initialMarginCollateralPortfolioCode = "NOAP"
            then collateral -> variationMarginCollateralPortfolioCode exists
            else if collateral -> collateralPortfolioIndicator = False
            then collateral -> variationMarginCollateralPortfolioCode is absent
            else if collateral -> uti exists
                    and collateral -> variationMarginCollateralPortfolioCode = "NOAP"
            then False)

    condition DTCC_JFSA_BR_0104_04: <"Event timestamp Condition 4">
        [docReference JFSA DTCC_Specs dataElement "104" field "Event timestamp"
          provision "The date value in the [Event timestamp] must be equal to or before the date value in the [Reporting timestamp]. [Event timestamp] should not be before the [Initial Event timestamp] of the NEWT Message."]
        [regulatoryReference ISDA ISDAWorkingGroup date "unknown"
            provision "The logic regarding the [Initial event timestamp] is not modelled. The condition cannot be executed in a stateless environment."]
        if collateral -> actionType = MarginActionEnum -> MARU
        then (collateral -> eventTimestamp <= partiesToTheDerivative -> reportingTimestamp)

type PartiesToTheDerivative:
    reportingTimestamp zonedDateTime (1..1)
        [ruleReference ReportingTimestamp]
    submitterIdentifier string (1..1)
        [ruleReference SubmitterIdentifier]
    entityResponsibleForReporting LEIIdentifier (0..1)
        [ruleReference EntityResponsibleForReporting]
    counterparty1 LEIIdentifier (1..1)
        [ruleReference Counterparty1]
    counterparty2IdentifierType boolean (0..1)
        [ruleReference Counterparty2IdentifierType]
    counterparty2 Min20Max72AlphaNumericText (0..1)
        [ruleReference Counterparty2]

type CollateralReport:
    collateralPortfolioIndicator boolean (0..1)
        [ruleReference CollateralPortfolioIndicator]
    uti CollateralUTIString (0..1)
        [ruleReference UTI]
    collateralisationCategory CollateralisationType3Code__1 (0..1) 
        [ruleReference CollateralisationCategory]
    initialMarginCollateralPortfolioCode Max52AlphaNumericText (0..1)
        [ruleReference InitialMarginCollateralPortfolioCode]
    initialMarginPostedByTheReportingCounterpartyPreHaircut ShortFraction5DecimalNumber (0..1)
        [ruleReference InitialMarginPostedByTheReportingCounterpartyPreHaircut]
    initialMarginPostedByTheReportingCounterpartyPostHaircut ShortFraction5DecimalNumber (0..1)
        [ruleReference InitialMarginPostedByTheReportingCounterpartyPostHaircut]
    currencyofInitialMarginPosted ISOCurrencyCodeEnum (0..1)
        [ruleReference CurrencyofInitialMarginPosted]
    initialMarginCollectedByReportingCounterpartyPreHaircut ShortFraction5DecimalNumber (0..1)
        [ruleReference InitialMarginCollectedByReportingCounterpartyPreHaircut]
    initialMarginCollectedByReportingCounterpartyPostHaircut ShortFraction5DecimalNumber (0..1)
        [ruleReference InitialMarginCollectedByReportingCounterpartyPostHaircut]
    currencyofInitialMarginCollected ISOCurrencyCodeEnum (0..1)
        [ruleReference CurrencyofInitialMarginCollected]
    variationMarginCollateralPortfolioCode Max52AlphaNumericText (0..1)
        [ruleReference VariationMarginCollateralPortfolioCode]
    variationMarginPostedByTheReportingCounterpartyPreHaircut ShortFraction5DecimalNumber (0..1)
        [ruleReference VariationMarginPostedByTheReportingCounterpartyPreHaircut]
    variationMarginPostedByTheReportingCounterpartyPostHaircut ShortFraction5DecimalNumber (0..1)
        [ruleReference VariationMarginPostedByTheReportingCounterpartyPostHaircut]
    currencyOfVariationMarginPosted ISOCurrencyCodeEnum (0..1)
        [ruleReference CurrencyOfVariationMarginPosted]
    variationMarginCollectedByTheReportingCounterpartyPreHaircut ShortFraction5DecimalNumber (0..1)
        [ruleReference VariationMarginCollectedByTheReportingCounterpartyPreHaircut]
    variationMarginCollectedByTheReportingCounterpartyPostHaircut ShortFraction5DecimalNumber (0..1)
        [ruleReference VariationMarginCollectedByTheReportingCounterpartyPostHaircut]
    currencyOfVariationMarginCollected ISOCurrencyCodeEnum (0..1)
        [ruleReference CurrencyOfVariationMarginCollected]
    excessCollateralPostedByTheReportingCounterparty ShortFraction5DecimalNumber (0..1)
        [ruleReference ExcessCollateralPostedByTheReportingCounterparty]
    currencyOfTheExcessCollateralPosted string (0..1)
        [ruleReference CurrencyOfTheExcessCollateralPosted]
    excessCollateralCollectedByTheReportingCounterparty ShortFraction5DecimalNumber (0..1)
        [ruleReference ExcessCollateralCollectedByTheReportingCounterparty]
    currencyOfTheExcessCollateralCollected string (0..1)
        [ruleReference CurrencyOfExcessCollateralCollected]
    eventTimestamp zonedDateTime (0..1)
        [ruleReference EventTimestamp]
    actionType MarginActionEnum (0..1)
        [ruleReference ActionType]
    technicalRecordId string (0..1)
        [ruleReference TechnicalRecordId]
