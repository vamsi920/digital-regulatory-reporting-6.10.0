namespace drr.regulation.asic.rewrite.margin
version "${project.version}"

import cdm.base.*
import cdm.base.staticdata.asset.common.*

import drr.regulation.asic.*
import drr.regulation.common.*
import drr.standards.iso.*

type ASICMarginReport:
    [rootType]
    uniqueTransactionIdentifier Max52AlphaNumericText (0..1)
        [ruleReference UniqueTransactionIdentifier]
    uniqueTransactionIdentifierProprietary UTIProprietaryIDFormat (0..1)
        [ruleReference UniqueTransactionIdentifierProprietary]
    reportingEntity string (1..1)
        [ruleReference ReportingEntity]
    counterparty1 Min20Max72AlphaNumericText (1..1)
        [ruleReference Counterparty1]
    smallScaleBuySideEntityIndicator string (1..1)
        [ruleReference SmallScaleBuySideEntityIndicator]
    counterparty2 Min20Max72AlphaNumericText (1..1)
        [ruleReference Counterparty2]
    collateralTimestamp zonedDateTime (0..1)
        [ruleReference CollateralTimestamp]
    collateralisationCategory CollateralisationType3Code__1 (0..1) 
        [ruleReference CollateralisationCategory]
    collateralPortfolioIndicator boolean (0..1)
        [ruleReference CollateralPortfolioIndicator]
    portfolioContainingNonReportedComponentIndicator boolean (0..1)
        [ruleReference PortfolioContainingNonReportedComponentIndicator]
    initialMarginCollateralPortfolioCode Max52AlphaNumericText (0..1)
        [ruleReference CollateralPortfolioCodeIM]
    collateralPortfolioCodeVM Max52AlphaNumericText (0..1)
        [ruleReference CollateralPortfolioCodeVM]
    initialMarginPostedByTheReportingEntityPreHaircut ShortFraction5DecimalNumber (0..1)
        [ruleReference InitialMarginPostedByTheReportingEntityPreHaircut]
    initialMarginPostedByTheReportingEntityPostHaircut ShortFraction5DecimalNumber (0..1)
        [ruleReference InitialMarginPostedByTheReportingEntityPostHaircut]
    currencyofInitialMarginPosted ISOCurrencyCodeEnum (0..1)
        [ruleReference CurrencyOfInitialMarginPosted]
    initialMarginCollectedByTheReportingEntityPreHaircut ShortFraction5DecimalNumber (0..1)
        [ruleReference InitialMarginCollectedByTheReportingEntityPreHaircut]
    initialMarginCollectedByTheReportingEntityPostHaircut ShortFraction5DecimalNumber (0..1)
        [ruleReference InitialMarginCollectedByTheReportingEntityPostHaircut]
    currencyofInitialMarginCollected ISOCurrencyCodeEnum (0..1)
        [ruleReference CurrencyOfInitialMarginCollected]
    variationMarginPostedByTheReportingEntityPreHaircut ShortFraction5DecimalNumber (0..1)
        [ruleReference VariationMarginPostedByTheReportingEntityPreHaircut]
    currencyOfVariationMarginPosted ISOCurrencyCodeEnum (0..1)
        [ruleReference CurrencyOfVariationMarginPosted]
    variationMarginCollectedByTheReportingEntityPreHaircut ShortFraction5DecimalNumber (0..1)
        [ruleReference VariationMarginCollectedByTheReportingEntityPreHaircut]
    currencyOfVariationMarginCollected ISOCurrencyCodeEnum (0..1)
        [ruleReference CurrencyOfVariationMarginCollected]
    actionType MarginActionEnum (0..1)
        [ruleReference ActionType]
    reportingTimestamp zonedDateTime (1..1)
        [ruleReference ReportingTimestamp]
    reportSubmittingEntityID string (1..1)
        [ruleReference ReportingSubmittingEntity]
    counterpartyIdentifierType boolean (1..1)
        [nonReportable]
        [ruleReference Counterparty2IdentifierType]
    technicalRecordId string (1..1)
        [ruleReference TechnicalRecordId]
    executionAgentOfCounterparty1 LEIIdentifier (1..1)
        [ruleReference ExecutionAgentOfCounterparty1]

    condition DTCC_ASIC_BR_3001_01: <"Unique transaction identifier">
        [docReference ASIC DTCC_Specs table "3" dataElement "1" validationRule "Collateral"
        provision "when [Action Type] = MARU, CORR then Required if [Initial margin collateral portfolio code] = 'NOAP' and [Variation margin collateral portfolio code] is = 'NOAP' or blank. (Hence required if [Collateral portfolio indicator] = False)
            Reject if [Collateral portfolio indicator] = True."]
        if actionType = MarginActionEnum -> MARU or actionType = MarginActionEnum -> CORR
        then if initialMarginCollateralPortfolioCode = "NOAP"
                    and collateralPortfolioCodeVM = "NOAP"
                    and collateralPortfolioIndicator = False
            then required choice uniqueTransactionIdentifier, uniqueTransactionIdentifierProprietary
            else if collateralPortfolioIndicator = True
            then (uniqueTransactionIdentifier is absent and uniqueTransactionIdentifierProprietary is absent)

    condition DTCC_ASIC_BR_3006_01: <"Collateral Timestamp">
        [docReference ASIC DTCC_Specs table "3" dataElement "6" validationRule "Collateral"
        provision "when [Action Type] = MARU, CORR then The date value in the [Collateral timestamp] must be equal to or before the date value in the [Reporting timestamp].
            [Collateral timestamp] should not be before the [Initial Event timestamp] of the NEWT Message."]
        [regulatoryReference ISDA ISDAWorkingGroup date "unknown"
            provision "Not modelled. The condition cannot be executed in a stateless environment."]
        if actionType = MarginActionEnum -> MARU or actionType = MarginActionEnum -> CORR
        then collateralTimestamp -> date <= reportingTimestamp -> date

    condition DTCC_ASIC_BR_3009_01: <"Portfolio containing non-reported component indicator">
        [docReference ASIC DTCC_Specs table "3" dataElement "9" validationRule "Collateral"
        provision "when [Action Type] = MARU, CORR then Required if [Collateral portfolio indicator] = True"]
        if actionType = MarginActionEnum -> MARU or actionType = MarginActionEnum -> CORR
        then if collateralPortfolioIndicator = True
            then portfolioContainingNonReportedComponentIndicator exists

    condition DTCC_ASIC_BR_3010_01: <"Collateral portfolio code (IM)">
        [docReference ASIC DTCC_Specs table "3" dataElement "10" validationRule "Collateral"
        provision "when [Action Type] = NEWT, MODI, CORR, REVI then Required if [Collateral portfolio code (variation margin)] = 'NOAP'.
            Reject if the value [NOAP]  is submitted in '/Prtfl/Cd'.
            Note: If the [Collateral portfolio code (initial margin)] is not applicable to your trade, clients could submit the value [NOAP] to represent 'No Applicable' in '/NoPrtfl' XML."]
        if actionType <> MarginActionEnum -> MARU
        then if initialMarginCollateralPortfolioCode = "NOAP"
            then False

    condition DTCC_ASIC_BR_3010_02: <"Collateral portfolio code (IM)">
        [docReference ASIC DTCC_Specs table "3" dataElement "10" validationRule "Collateral"
        provision "when [Action Type] = MARU, CORR then Required if [Collateral portfolio code (variation margin)] = 'NOAP' and [Unique transaction identifier (UTI)] is blank.
        Reject if the value [NOAP]  is submitted in '/Prtfl/Cd'.
        Reject if [UTI] is populated and [Collateral portfolio code (initial margin)] does NOT = 'NOAP'.
        Note: If the [Collateral portfolio code (initial margin)] is not applicable to your trade, clients could submit the value [NOAP] to represent 'No Applicable' in '/NoPrtfl' XML."]
        if actionType = MarginActionEnum -> MARU or actionType = MarginActionEnum -> CORR
        then if (uniqueTransactionIdentifier exists or uniqueTransactionIdentifierProprietary exists)
                    and initialMarginCollateralPortfolioCode <> "NOAP"
            then False

    condition DTCC_ASIC_BR_3011_01: <"Collateral portfolio code (VM)">
        [docReference ASIC DTCC_Specs table "3" dataElement "11" validationRule "Collateral"
        provision "when [Action Type] = NEWT, MODI, CORR, REVI then Reject if the value [NOAP]  is submitted in '/Prtfl/Cd'.
        Note: If the [Collateral portfolio code (variation margin)] is not applicable to your trade, clients should submit the value [NOAP] to represent 'No Applicable' in '/NoPrtfl' XML."]
        if actionType <> MarginActionEnum -> MARU
        then if initialMarginCollateralPortfolioCode = "NOAP"
            then collateralPortfolioCodeVM exists

    condition DTCC_ASIC_BR_3011_02: <"Collateral portfolio code (VM)">
        [docReference ASIC DTCC_Specs table "3" dataElement "11" validationRule "Collateral"
        provision "when [Action Type] = MARU, CORR then Reject if [UTI] is populated and [Collateral portfolio code (variation margin)] does NOT = 'NOAP'.
            Reject if the value [NOAP]  is submitted in '/Prtfl/Cd'.
            Note: If the [Collateral portfolio code (variation margin)] is not applicable to your trade, clients should submit the value [NOAP] to represent 'No Applicable' in '/NoPrtfl' XML."]
        if actionType = MarginActionEnum -> MARU
                or actionType = MarginActionEnum -> CORR
                    and initialMarginCollateralPortfolioCode = "NOAP"
                    and (uniqueTransactionIdentifier is absent and uniqueTransactionIdentifierProprietary is absent)
        then variationMarginCollectedByTheReportingEntityPreHaircut exists
        else if (uniqueTransactionIdentifier exists or uniqueTransactionIdentifierProprietary exists)
                    and collateralPortfolioCodeVM <> "NOAP"
                or initialMarginCollateralPortfolioCode = "NOAP"
        then collateralPortfolioCodeVM is absent

    condition DTCC_ASIC_BR_3012_01: <"Initial margin posted by the Reporting Entity (pre-haircut)">
        [docReference ASIC DTCC_Specs table "3" dataElement "12" validationRule "Collateral"
        provision "when [Action Type] = MARU, CORR then Required if [Collateralisation category] = OWC1 or OWP1 or FLCL.
        Reject if [Collateralisation category] does not equal to OWC1 or OWP1 or FLCL.
        Reject if Negative"]
        if actionType = MarginActionEnum -> MARU or actionType = MarginActionEnum -> CORR
        then if collateralisationCategory = CollateralisationType3Code__1 -> OWC1
                    or collateralisationCategory = CollateralisationType3Code__1 -> OWP1
                    or collateralisationCategory = CollateralisationType3Code__1 -> FLCL
            then initialMarginPostedByTheReportingEntityPreHaircut exists and initialMarginPostedByTheReportingEntityPreHaircut >= 0
            else initialMarginPostedByTheReportingEntityPreHaircut is absent

    condition DTCC_ASIC_BR_3013_01: <"Initial margin posted by the Reporting Entity (post-haircut)">
        [docReference ASIC DTCC_Specs table "3" dataElement "13" validationRule "Collateral"
        provision "when [Action Type] = MARU, CORR then Required if [Collateralisation category] = OWC1 or OWP1 or FLCL.
        Reject if [Collateralisation category] does not equal to OWC1 or OWP1 or FLCL.
        Reject if Negative"]
        if actionType = MarginActionEnum -> MARU or actionType = MarginActionEnum -> CORR
        then if collateralisationCategory = CollateralisationType3Code__1 -> OWC1
                    or collateralisationCategory = CollateralisationType3Code__1 -> OWP1
                    or collateralisationCategory = CollateralisationType3Code__1 -> FLCL
            then initialMarginCollectedByTheReportingEntityPostHaircut exists and initialMarginCollectedByTheReportingEntityPostHaircut >= 0
            else initialMarginCollectedByTheReportingEntityPostHaircut is absent

    condition DTCC_ASIC_BR_3014_01: <"Currency of initial margin posted">
        [docReference ASIC DTCC_Specs table "3" dataElement "14" validationRule "Collateral"
        provision "when [Action Type] = MARU, CORR then Required if [Initial margin posted by the reporting counterparty (pre-haircut)] is populated.
        Reject if [Currency of initial margin posted (pre-haircut)] = XAG, XAU, XBA, XBB, XBC, XBD, XDR, XEU, XFU, XPD, XPT, XTS, XXX."]
        if actionType = MarginActionEnum -> MARU or actionType = MarginActionEnum -> CORR
        then if initialMarginPostedByTheReportingEntityPreHaircut exists
            then currencyofInitialMarginPosted exists
                    and [ISOCurrencyCodeEnum -> XAG, ISOCurrencyCodeEnum -> XAU, ISOCurrencyCodeEnum -> XBA, ISOCurrencyCodeEnum -> XBB, ISOCurrencyCodeEnum -> XBC, ISOCurrencyCodeEnum -> XBD, ISOCurrencyCodeEnum -> XDR, ISOCurrencyCodeEnum -> XPD, ISOCurrencyCodeEnum -> XPT, ISOCurrencyCodeEnum -> XXX, ISOCurrencyCodeEnum -> XTS] any <> currencyofInitialMarginPosted

    condition DTCC_ASIC_BR_3014_02: <"Currency of initial margin posted">
        [docReference ASIC DTCC_Specs table "3" dataElement "14" validationRule "Collateral"
        provision "when [Action Type] = MARU, CORR then Required if [Initial margin posted by the reporting counterparty (post- haircut)] is populated.
        Reject if [Currency of initial margin posted (post-haircut)] = XAG, XAU, XBA, XBB, XBC, XBD, XDR, XEU, XFU, XPD, XPT, XTS, XXX."]
        if actionType = MarginActionEnum -> MARU or actionType = MarginActionEnum -> CORR
        then if initialMarginPostedByTheReportingEntityPostHaircut exists
            then currencyofInitialMarginPosted exists
                    and [ISOCurrencyCodeEnum -> XAG, ISOCurrencyCodeEnum -> XAU, ISOCurrencyCodeEnum -> XBA, ISOCurrencyCodeEnum -> XBB, ISOCurrencyCodeEnum -> XBC, ISOCurrencyCodeEnum -> XBD, ISOCurrencyCodeEnum -> XDR, ISOCurrencyCodeEnum -> XPD, ISOCurrencyCodeEnum -> XPT, ISOCurrencyCodeEnum -> XTS, ISOCurrencyCodeEnum -> XXX] any <> currencyofInitialMarginPosted

    condition DTCC_ASIC_BR_3015_01: <"Initial margin collected by the Reporting Entity (pre-haircut)">
        [docReference ASIC DTCC_Specs table "3" dataElement "15" validationRule "Collateral"
        provision "when [Action Type] = MARU, CORR then Required if [Collateralisation category] = OWC1 or OWP1 or FLCL.
        Reject if [Collateralisation category] does not equal to OWC1 or OWP1 or FLCL.
        Reject if Negative"]
        if actionType = MarginActionEnum -> MARU or actionType = MarginActionEnum -> CORR
        then if (collateralisationCategory = CollateralisationType3Code__1 -> OWC1 or collateralisationCategory = CollateralisationType3Code__1 -> OWP1 or collateralisationCategory = CollateralisationType3Code__1 -> FLCL)
            then initialMarginCollectedByTheReportingEntityPreHaircut exists and initialMarginCollectedByTheReportingEntityPreHaircut >= 0
            else if (collateralisationCategory <> CollateralisationType3Code__1 -> OWC1 or collateralisationCategory <> CollateralisationType3Code__1 -> OWP1 or collateralisationCategory <> CollateralisationType3Code__1 -> FLCL)
            then initialMarginCollectedByTheReportingEntityPreHaircut is absent

    condition DTCC_ASIC_BR_3016_01: <"Initial margin collected by the Reporting Entity (post-haircut)">
        [docReference ASIC DTCC_Specs table "3" dataElement "16" validationRule "Collateral"
        provision "when [Action Type] = MARU, CORR then Required if [Small-scale Buy-side Entity indicator] is NOT equal to 'SSBS' and [Collateralisation category] = OWC1 or OWP1 or FLCL.
        Reject if [Collateralisation category] does not equal to OWC1 or OWP1 or FLCL.
        Reject if Negative"]
        if actionType = MarginActionEnum -> MARU or actionType = MarginActionEnum -> CORR
        then if (smallScaleBuySideEntityIndicator <> "SSBS" and (collateralisationCategory = CollateralisationType3Code__1 -> OWC1 or collateralisationCategory = CollateralisationType3Code__1 -> OWP1 or collateralisationCategory = CollateralisationType3Code__1 -> FLCL))
            then initialMarginCollectedByTheReportingEntityPostHaircut exists and initialMarginCollectedByTheReportingEntityPostHaircut >= 0
            else if (collateralisationCategory <> CollateralisationType3Code__1 -> OWC1 or collateralisationCategory <> CollateralisationType3Code__1 -> OWP1 or collateralisationCategory <> CollateralisationType3Code__1 -> FLCL)
            then initialMarginCollectedByTheReportingEntityPostHaircut is absent

    condition DTCC_ASIC_BR_3017_01: <"Currency of initial margin collected">
        [docReference ASIC DTCC_Specs table "3" dataElement "17" validationRule "Collateral"
        provision "when [Action Type] = MARU, CORR then Required if [Initial margin collected by the reporting counterparty (pre-haircut)] is populated.
        Reject if [Currency of initial margin collected (pre-haircut)] = XAG, XAU, XBA, XBB, XBC, XBD, XDR, XEU, XFU, XPD, XPT, XTS, XXX."]
        if actionType = MarginActionEnum -> MARU or actionType = MarginActionEnum -> CORR
        then if initialMarginCollectedByTheReportingEntityPreHaircut exists
            then currencyofInitialMarginCollected exists
                    and [ISOCurrencyCodeEnum -> XAG, ISOCurrencyCodeEnum -> XAU, ISOCurrencyCodeEnum -> XBA, ISOCurrencyCodeEnum -> XBB, ISOCurrencyCodeEnum -> XBC, ISOCurrencyCodeEnum -> XBD, ISOCurrencyCodeEnum -> XDR, ISOCurrencyCodeEnum -> XPD, ISOCurrencyCodeEnum -> XPT, ISOCurrencyCodeEnum -> XXX, ISOCurrencyCodeEnum -> XTS] any <> currencyofInitialMarginCollected

    condition DTCC_ASIC_BR_3017_02: <"Currency of initial margin collected">
        [docReference ASIC DTCC_Specs table "3" dataElement "17" validationRule "Collateral"
        provision "when [Action Type] = MARU, CORR then Required if [Initial margin collected by the reporting counterparty (post-haircut)] is populated.
        Reject if [Currency of initial margin collected (post-haircut)] = XAG, XAU, XBA, XBB, XBC, XBD, XDR, XEU, XFU, XPD, XPT, XTS, XXX."]
        if actionType = MarginActionEnum -> MARU or actionType = MarginActionEnum -> CORR
        then if initialMarginCollectedByTheReportingEntityPostHaircut exists
            then currencyofInitialMarginCollected exists
                    and [ISOCurrencyCodeEnum -> XAG, ISOCurrencyCodeEnum -> XAU, ISOCurrencyCodeEnum -> XBA, ISOCurrencyCodeEnum -> XBB, ISOCurrencyCodeEnum -> XBC, ISOCurrencyCodeEnum -> XBD, ISOCurrencyCodeEnum -> XDR, ISOCurrencyCodeEnum -> XPD, ISOCurrencyCodeEnum -> XPT, ISOCurrencyCodeEnum -> XXX, ISOCurrencyCodeEnum -> XTS] any <> currencyofInitialMarginCollected

    condition DTCC_ASIC_BR_3018_01: <"Variation margin posted by the Reporting Entity (pre-haircut)">
        [docReference ASIC DTCC_Specs table "3" dataElement "18" validationRule "Collateral"
        provision "when [Action Type] = MARU, CORR then Required if [Collateralisation category] = PRC1 or PRCL or OWC1 or OWP1 or OWP2 or FLCL.
        Reject if [Collateralisation category] does not equal to PRC1 or PRCL or OWC1 or OWP1 or OWP2 or FLCL.
        Reject if Negative"]
        if actionType = MarginActionEnum -> MARU or actionType = MarginActionEnum -> CORR
        then if collateralisationCategory = CollateralisationType3Code__1 -> PRC1
                    or collateralisationCategory = CollateralisationType3Code__1 -> PRCL
                    or collateralisationCategory = CollateralisationType3Code__1 -> OWC1
                    or collateralisationCategory = CollateralisationType3Code__1 -> OWP1
                    or collateralisationCategory = CollateralisationType3Code__1 -> OWP2
                    or collateralisationCategory = CollateralisationType3Code__1 -> FLCL
            then variationMarginPostedByTheReportingEntityPreHaircut exists and variationMarginPostedByTheReportingEntityPreHaircut >= 0
            else variationMarginPostedByTheReportingEntityPreHaircut is absent

    condition DTCC_ASIC_BR_3019_01: <"Currency of variation margin posted">
        [docReference ASIC DTCC_Specs table "3" dataElement "19" validationRule "Collateral"
        provision "when [Action Type] = MARU then Required if [Variation margin posted by the Reporting Entity (pre-haircut)] is populated.
        Reject if [Currency of variation margin posted] = XAG, XAU, XBA, XBB, XBC, XBD, XDR, XEU, XFU, XPD, XPT, XTS, XXX."]
        if actionType = MarginActionEnum -> MARU
        then if variationMarginPostedByTheReportingEntityPreHaircut exists
            then currencyOfVariationMarginPosted exists
                    and [ISOCurrencyCodeEnum -> XAG, ISOCurrencyCodeEnum -> XAU, ISOCurrencyCodeEnum -> XBA, ISOCurrencyCodeEnum -> XBB, ISOCurrencyCodeEnum -> XBC, ISOCurrencyCodeEnum -> XBD, ISOCurrencyCodeEnum -> XDR, ISOCurrencyCodeEnum -> XPD, ISOCurrencyCodeEnum -> XPT, ISOCurrencyCodeEnum -> XTS, ISOCurrencyCodeEnum -> XXX] any <> currencyOfVariationMarginPosted

    condition DTCC_ASIC_BR_3020_01: <"Variation margin collected by the Reporting Entity (pre-haircut)">
        [docReference ASIC DTCC_Specs table "3" dataElement "20" validationRule "Collateral"
        provision "when [Action Type] = MARU, CORR then Required if [Small-scale Buy-side Entity indicator] is NOT equal to 'SSBS' and [Collateralisation category] = PRC2 or PRCL or OWC2 or OWP1 or OWP2 or FLCL.
        Reject if [Small-scale Buy-side Entity indicator] is NOT equal to 'SSBS and if [Collateralisation category] does not equal to PRC2 or PRCL or OWC2 or OWP1 or OWP2 or FLCL.
        Reject if Negative"]
        if actionType = MarginActionEnum -> MARU or actionType = MarginActionEnum -> CORR
        then if (smallScaleBuySideEntityIndicator <> "SSBS" and (collateralisationCategory = CollateralisationType3Code__1 -> PRC2 or collateralisationCategory = CollateralisationType3Code__1 -> PRCL or collateralisationCategory = CollateralisationType3Code__1 -> OWC2 or collateralisationCategory = CollateralisationType3Code__1 -> OWP1 or collateralisationCategory = CollateralisationType3Code__1 -> OWP2 or collateralisationCategory = CollateralisationType3Code__1 -> FLCL))
            then variationMarginCollectedByTheReportingEntityPreHaircut exists and variationMarginCollectedByTheReportingEntityPreHaircut >= 0
            else if (smallScaleBuySideEntityIndicator <> "SSBS" and (collateralisationCategory <> CollateralisationType3Code__1 -> PRC2 or collateralisationCategory <> CollateralisationType3Code__1 -> PRCL or collateralisationCategory <> CollateralisationType3Code__1 -> OWC2 or collateralisationCategory <> CollateralisationType3Code__1 -> OWP1 or collateralisationCategory <> CollateralisationType3Code__1 -> OWP2 or collateralisationCategory <> CollateralisationType3Code__1 -> FLCL))
            then variationMarginCollectedByTheReportingEntityPreHaircut is absent

    condition DTCC_ASIC_BR_3021_01: <"Currency of variation margin collected">
        [docReference ASIC DTCC_Specs table "3" dataElement "21" validationRule "Collateral"
        provision "when [Action Type] = MARU, CORR then Required if [Variation margin collected by the Reporting Entity (pre-haircut)] is populated.
        Reject if [Currency of variation margin collected] = XAG, XAU, XBA, XBB, XBC, XBD, XDR, XEU, XFU, XPD, XPT, XTS, XXX."]
        if actionType = MarginActionEnum -> MARU or actionType = MarginActionEnum -> CORR
        then if variationMarginCollectedByTheReportingEntityPreHaircut exists
            then currencyOfVariationMarginCollected exists
                    and [ISOCurrencyCodeEnum -> XAG, ISOCurrencyCodeEnum -> XAU, ISOCurrencyCodeEnum -> XBA, ISOCurrencyCodeEnum -> XBB, ISOCurrencyCodeEnum -> XBC, ISOCurrencyCodeEnum -> XBD, ISOCurrencyCodeEnum -> XDR, ISOCurrencyCodeEnum -> XPD, ISOCurrencyCodeEnum -> XPT, ISOCurrencyCodeEnum -> XTS, ISOCurrencyCodeEnum -> XXX] any <> currencyOfVariationMarginCollected

    condition DTCC_ASIC_BR_3023_01: <"Reporting Timestamp">
        [docReference ASIC DTCC_Specs table "3" dataElement "23" validationRule "Collateral"
        provision "when [Action Type] = MARU, CORR then [Reporting timestamp] must be equal to or before the [Message Received timestamp] derived by the TR.
            Reject if the date value of the [Reporting timestamp] is more than 2 days before the date value of the [Message Received timestamp]."]
        if actionType = MarginActionEnum -> MARU or actionType = MarginActionEnum -> CORR
        then if variationMarginCollectedByTheReportingEntityPreHaircut exists
            then True

// The logic regarding the [Message Received timestamp] is not included as this data is known post submission.
    condition DTCC_ASIC_BR_0013_02: <"Execution agent of the Counterparty 1">
        [docReference ASIC DTCC_Specs dataElement "N/A" validationRule "Transaction"
        provision "When [Action Type] = MARU/CORR then [Execution agent of Counterparty 1] must NOT = [Counterparty 1] or [Counterparty 2] or [Reporting Entity]"]
        [docReference ISDA APACJFSAPeerReviewGroup date "20230515"
        provision "DTCC has provided clarification stating that the [Reporting Entity] in business rules for data field Execution Agent of the Counterparty 1 refers to [Data Submitter] field in MAS message spec v0.4."]
        if [MarginActionEnum -> CORR, MarginActionEnum -> MARU] any = actionType
        then executionAgentOfCounterparty1 <> counterparty1
                or executionAgentOfCounterparty1 <> counterparty2
                or executionAgentOfCounterparty1 <> reportingEntity
